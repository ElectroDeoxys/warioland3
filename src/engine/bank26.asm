TreasureGfx:
Treasure000Gfx:
INCBIN "gfx/treasures/treasure_000.2bpp"

Treasure001Gfx:
INCBIN "gfx/treasures/treasure_001.2bpp"

Treasure002Gfx:
INCBIN "gfx/treasures/treasure_002.2bpp"

Treasure003Gfx:
INCBIN "gfx/treasures/treasure_003.2bpp"

Treasure004Gfx:
INCBIN "gfx/treasures/treasure_004.2bpp"

Treasure005Gfx:
INCBIN "gfx/treasures/treasure_005.2bpp"

Treasure006Gfx:
INCBIN "gfx/treasures/treasure_006.2bpp"

Treasure007Gfx:
INCBIN "gfx/treasures/treasure_007.2bpp"

Treasure008Gfx:
INCBIN "gfx/treasures/treasure_008.2bpp"

Treasure009Gfx:
INCBIN "gfx/treasures/treasure_009.2bpp"

Treasure010Gfx:
INCBIN "gfx/treasures/treasure_010.2bpp"

Treasure011Gfx:
INCBIN "gfx/treasures/treasure_011.2bpp"

Treasure012Gfx:
INCBIN "gfx/treasures/treasure_012.2bpp"

Treasure013Gfx:
INCBIN "gfx/treasures/treasure_013.2bpp"

Treasure014Gfx:
INCBIN "gfx/treasures/treasure_014.2bpp"

Treasure015Gfx:
INCBIN "gfx/treasures/treasure_015.2bpp"

Treasure016Gfx:
INCBIN "gfx/treasures/treasure_016.2bpp"

Treasure017Gfx:
INCBIN "gfx/treasures/treasure_017.2bpp"

Treasure018Gfx:
INCBIN "gfx/treasures/treasure_018.2bpp"

Treasure019Gfx:
INCBIN "gfx/treasures/treasure_019.2bpp"

Treasure020Gfx:
INCBIN "gfx/treasures/treasure_020.2bpp"

Treasure021Gfx:
INCBIN "gfx/treasures/treasure_021.2bpp"

Treasure022Gfx:
INCBIN "gfx/treasures/treasure_022.2bpp"

Treasure023Gfx:
INCBIN "gfx/treasures/treasure_023.2bpp"

Treasure024Gfx:
INCBIN "gfx/treasures/treasure_024.2bpp"

Treasure025Gfx:
INCBIN "gfx/treasures/treasure_025.2bpp"

Treasure026Gfx:
INCBIN "gfx/treasures/treasure_026.2bpp"

Treasure027Gfx:
INCBIN "gfx/treasures/treasure_027.2bpp"

Treasure028Gfx:
INCBIN "gfx/treasures/treasure_028.2bpp"

Treasure029Gfx:
INCBIN "gfx/treasures/treasure_029.2bpp"

Treasure030Gfx:
INCBIN "gfx/treasures/treasure_030.2bpp"

Treasure031Gfx:
INCBIN "gfx/treasures/treasure_031.2bpp"

Treasure032Gfx:
INCBIN "gfx/treasures/treasure_032.2bpp"

Treasure033Gfx:
INCBIN "gfx/treasures/treasure_033.2bpp"

Treasure034Gfx:
INCBIN "gfx/treasures/treasure_034.2bpp"

Treasure035Gfx:
INCBIN "gfx/treasures/treasure_035.2bpp"

Treasure036Gfx:
INCBIN "gfx/treasures/treasure_036.2bpp"

Treasure037Gfx:
INCBIN "gfx/treasures/treasure_037.2bpp"

Treasure038Gfx:
INCBIN "gfx/treasures/treasure_038.2bpp"

Treasure039Gfx:
INCBIN "gfx/treasures/treasure_039.2bpp"

Treasure040Gfx:
INCBIN "gfx/treasures/treasure_040.2bpp"

Treasure041Gfx:
INCBIN "gfx/treasures/treasure_041.2bpp"

Treasure042Gfx:
INCBIN "gfx/treasures/treasure_042.2bpp"

Treasure043Gfx:
INCBIN "gfx/treasures/treasure_043.2bpp"

Treasure044Gfx:
INCBIN "gfx/treasures/treasure_044.2bpp"

Treasure045Gfx:
INCBIN "gfx/treasures/treasure_045.2bpp"

Treasure046Gfx:
INCBIN "gfx/treasures/treasure_046.2bpp"

Treasure047Gfx:
INCBIN "gfx/treasures/treasure_047.2bpp"

Treasure048Gfx:
INCBIN "gfx/treasures/treasure_048.2bpp"

Treasure049Gfx:
INCBIN "gfx/treasures/treasure_049.2bpp"

Treasure050Gfx:
INCBIN "gfx/treasures/treasure_050.2bpp"

Treasure051Gfx:
INCBIN "gfx/treasures/treasure_051.2bpp"

Treasure052Gfx:
INCBIN "gfx/treasures/treasure_052.2bpp"

Treasure053Gfx:
INCBIN "gfx/treasures/treasure_053.2bpp"

Treasure054Gfx:
INCBIN "gfx/treasures/treasure_054.2bpp"

Treasure055Gfx:
INCBIN "gfx/treasures/treasure_055.2bpp"

Treasure056Gfx:
INCBIN "gfx/treasures/treasure_056.2bpp"

Treasure057Gfx:
INCBIN "gfx/treasures/treasure_057.2bpp"

Treasure058Gfx:
INCBIN "gfx/treasures/treasure_058.2bpp"

Treasure059Gfx:
INCBIN "gfx/treasures/treasure_059.2bpp"

Treasure060Gfx:
INCBIN "gfx/treasures/treasure_060.2bpp"

Treasure061Gfx:
INCBIN "gfx/treasures/treasure_061.2bpp"

Treasure062Gfx:
INCBIN "gfx/treasures/treasure_062.2bpp"

Treasure063Gfx:
INCBIN "gfx/treasures/treasure_063.2bpp"

Treasure064Gfx:
INCBIN "gfx/treasures/treasure_064.2bpp"

Treasure065Gfx:
INCBIN "gfx/treasures/treasure_065.2bpp"

Treasure066Gfx:
INCBIN "gfx/treasures/treasure_066.2bpp"

Treasure067Gfx:
INCBIN "gfx/treasures/treasure_067.2bpp"

Treasure068Gfx:
INCBIN "gfx/treasures/treasure_068.2bpp"

Treasure069Gfx:
INCBIN "gfx/treasures/treasure_069.2bpp"

Treasure070Gfx:
INCBIN "gfx/treasures/treasure_070.2bpp"

Treasure071Gfx:
INCBIN "gfx/treasures/treasure_071.2bpp"

Treasure072Gfx:
INCBIN "gfx/treasures/treasure_072.2bpp"

Treasure073Gfx:
INCBIN "gfx/treasures/treasure_073.2bpp"

Treasure074Gfx:
INCBIN "gfx/treasures/treasure_074.2bpp"

Treasure075Gfx:
INCBIN "gfx/treasures/treasure_075.2bpp"

Treasure076Gfx:
INCBIN "gfx/treasures/treasure_076.2bpp"

Treasure077Gfx:
INCBIN "gfx/treasures/treasure_077.2bpp"

Treasure078Gfx:
INCBIN "gfx/treasures/treasure_078.2bpp"

Treasure079Gfx:
INCBIN "gfx/treasures/treasure_079.2bpp"

Treasure080Gfx:
INCBIN "gfx/treasures/treasure_080.2bpp"

Treasure081Gfx:
INCBIN "gfx/treasures/treasure_081.2bpp"

Treasure082Gfx:
INCBIN "gfx/treasures/treasure_082.2bpp"

Treasure083Gfx:
INCBIN "gfx/treasures/treasure_083.2bpp"

Treasure084Gfx:
INCBIN "gfx/treasures/treasure_084.2bpp"

Treasure085Gfx:
INCBIN "gfx/treasures/treasure_085.2bpp"

Treasure086Gfx:
INCBIN "gfx/treasures/treasure_086.2bpp"

Treasure087Gfx:
INCBIN "gfx/treasures/treasure_087.2bpp"

Treasure088Gfx:
INCBIN "gfx/treasures/treasure_088.2bpp"

Treasure089Gfx:
INCBIN "gfx/treasures/treasure_089.2bpp"

Treasure090Gfx:
INCBIN "gfx/treasures/treasure_090.2bpp"

Treasure091Gfx:
INCBIN "gfx/treasures/treasure_091.2bpp"

Treasure092Gfx:
INCBIN "gfx/treasures/treasure_092.2bpp"

Treasure093Gfx:
INCBIN "gfx/treasures/treasure_093.2bpp"

Treasure094Gfx:
INCBIN "gfx/treasures/treasure_094.2bpp"

Treasure095Gfx:
INCBIN "gfx/treasures/treasure_095.2bpp"

Treasure096Gfx:
INCBIN "gfx/treasures/treasure_096.2bpp"

Treasure097Gfx:
INCBIN "gfx/treasures/treasure_097.2bpp"

Treasure098Gfx:
INCBIN "gfx/treasures/treasure_098.2bpp"

Treasure099Gfx:
INCBIN "gfx/treasures/treasure_099.2bpp"

Treasure100Gfx:
INCBIN "gfx/treasures/treasure_100.2bpp"

Treasure101Gfx:
INCBIN "gfx/treasures/treasure_101.2bpp"

NumbersGfx:
INCBIN "gfx/gui/numbers.2bpp"

TreasureCollectionGfx:
INCBIN "gfx/gui/treasure_collection.2bpp.rle"

TreasureCollectionCursorGfx:
INCBIN "gfx/gui/treasure_collection_cursor.2bpp.rle"

BGMap_9a129:
INCBIN "gfx/bgmaps/map_9a129.bin.rle"

BGMap_9a2ac:
INCBIN "gfx/bgmaps/map_9a2ac.bin.rle"

DrawCoinCount:
	ld de, v0Tiles1 + $a0
	ld a, [wNumCoins + 0]
	call .DrawNumber
	ld a, [wNumCoins + 1]
	swap a
	call .DrawNumber
	ld a, [wNumCoins + 1]
	call .DrawNumber
	ret

.DrawNumber
	and $0f
	inc a
	ld hl, NumbersGfx
	ld bc, $20
.loop
	dec a
	jr z, .got_number
	add hl, bc
	jr .loop
.got_number
	ld b, $20
	call CopyHLToDE
	ret

_InitTreasureCollection:
	call DisableLCD
	call FillBGMap0_With7f
	call ClearVirtualOAM

	play_music2 MUSIC_TREASURE_COLLECTION

	xor a
	ldh [rSCX], a
	ld [wSCX], a
	ldh [rSCY], a
	ld [wSCY], a

	ld a, $02
	ldh [rSVBK], a
	call VBlank_TreasureCollection
	call Func_9a54e
	call Func_9a559
	xor a
	ld [wHDMA], a

	; if collection is already open,
	; then we don't need to search for
	; the right page
	ld a, [wIsCollectionOpen]
	and a
	jr nz, .skip_jump_page

	lb bc, 0, 0
	ld a, [wTransitionParam]
	and a
	jr z, .got_page
	cp NUM_TREASURES + 1
	jr nc, .got_page
.loop_find_page
	cp NUM_COLLECTION_CELLS + 1
	jr c, .found_page
	inc b
	sub NUM_COLLECTION_CELLS
	jr .loop_find_page
.found_page
	dec a
	ld c, a
.got_page
	ld a, b
	ld [wCollectionPage], a
	ld a, c
	ld [wCollectionCell], a
	ld a, TRUE
	ld [wIsCollectionOpen], a

.skip_jump_page
	call GetCurrentCollectionCellRowAndColumn
	call GetCollectionCellCoords

	ld hl, Pals_9ad45
	call LoadPalsToTempPals1
	ld hl, Pals_9ad05
	call LoadPalsToTempPals2

	decompress_vram0 TreasureCollectionCursorGfx, v0Tiles0
	decompress_vram0 TreasureCollectionGfx, v0Tiles2

	call LoadCollectionPageTreasureGfx

	ld a, BANK("VRAM1")
	ldh [rVBK], a
	ld hl, NumbersGfx
	ld de, v1Tiles1 tile $20
	ld bc, $14 tiles
	call CopyHLToDE_BC
	xor a
	ldh [rVBK], a

	decompress_vram0 BGMap_9a2ac, wAttrmap
	decompress_vram0 BGMap_9a129, wTilemap

	call ApplyPageTreasuresPals

	ld hl, wAttrmap + $22
	ld a, $09
	ld [hli], a
	ld [hli], a
	ld [hl], a
	ld hl, wAttrmap + $42
	ld [hli], a
	ld [hli], a
	ld [hl], a

	ld a, [wCollectionPage]
	inc a
	add $20
	ld [wTilemap + $30], a

	ld hl, wTilemap
	ld de, v0BGMap1
	ld bc, 18 * BG_MAP_WIDTH
	call CopyHLToDE_BC
	ld a, BANK("VRAM1")
	ldh [rVBK], a
	ld hl, wAttrmap
	ld de, v0BGMap1
	ld bc, 18 * BG_MAP_WIDTH
	call CopyHLToDE_BC

	xor a
	ldh [rVBK], a
	hlbgcoord 2, 1, v0BGMap1
	farcall PrintNumCollectedTreasures

	ld hl, wSceneObj1State
	ld a, $01
	call SetSceneObjState
	ld b, $10
	ld a, $08
	ld hl, wSceneObj9XCoord
	ld [hld], a
	ld [hl], b
	ld hl, wSceneObj10XCoord
	ld a, $98
	ld [hld], a
	ld [hl], b
	ld a, LCDCF_ON | LCDCF_BG9C00 | LCDCF_OBJ16 | LCDCF_OBJON | LCDCF_BGON
	ldh [rLCDC], a
	ld hl, wSubState
	inc [hl]
	ret

LoadCollectionPageTreasureGfx:
	ld hl, Treasure001Gfx - (NUM_TREASURE_TILES * NUM_COLLECTION_CELLS) tiles
	ld de, (NUM_TREASURE_TILES * NUM_COLLECTION_CELLS) tiles
	ld a, [wCollectionPage]
	inc a
.loop
	add hl, de
	dec a
	jr nz, .loop
	ld de, v0Tiles1 tile $20
	ld bc, (NUM_TREASURE_TILES * NUM_COLLECTION_CELLS) tiles
	call CopyHLToDE_BC

	ld hl, Treasure101Gfx
	ld de, v0Tiles1 tile $7c
	ld b, NUM_TREASURE_TILES tiles
	call CopyHLToDE
	ret

Func_9a54e:
	ld hl, wCollectionRow
	ld bc, $800
	xor a
	call WriteAToHL_BCTimes
	ret

Func_9a559:
	ld hl, wCurSceneObj
	ld bc, $200
	xor a
	call WriteAToHL_BCTimes
	ret

VBlank_TreasureCollection:
	ld hl, .Func
	ld de, wVBlankFunc
	ld b, .end - .Func
	call CopyHLToDE
	ret

.Func:
	ld a, $02
	ldh [rSVBK], a
	ld a, [wSCY]
	ldh [rSCY], a
	ld a, [wSCX]
	ldh [rSCX], a
	xor a
	ldh [rVBK], a
	ld a, HIGH(wVirtualOAM)
	jp hTransferVirtualOAM
.end

; iterates all treasures in the current page
; and applies the corresponding palette
ApplyPageTreasuresPals:
	xor a
	ld [w2dffe], a
	ld a, [wCollectionPage]
	ld c, a
	inc c
	ld a, -NUM_COLLECTION_CELLS
.loop
	add NUM_COLLECTION_CELLS
	dec c
	jr nz, .loop
	inc a
	ld [w2dfff], a
.loop_treasures
	ld a, [w2dfff]
	call IsTreasureCollected
	jr z, .not_collected
	jr .collected

.next_treasure
	ld hl, w2dffe
	inc [hl]
	ld a, [hli]
	inc [hl] ; w2dfff
	cp NUM_COLLECTION_CELLS
	jr nz, .loop_treasures
	ret

.not_collected
	call .GetAttrmapAddress
	push de
	ld a, $fc
	ld [de], a
	inc a
	ld [hli], a
	inc a
	inc e
	ld [de], a
	inc a
	ld [hl], a
	pop hl
	dec h
	dec h
	dec h
	ld a, $04
	ld [hli], a
	ld [hl], a
	ld de, BG_MAP_WIDTH
	add hl, de
	ld [hld], a
	ld [hl], a
	jr .next_treasure

.collected
	ld a, [w2dfff]
	ld e, a
	ld d, $00
	ld hl, TreasureOBPals
	add hl, de
	ld a, [hl]
	push af
	call .GetAttrmapAddress
	pop af
	dec h
	dec h
	dec h
	ld [hli], a
	ld [hl], a
	dec d
	dec d
	dec d
	ld [de], a
	inc de
	ld [de], a
	jr .next_treasure

; outputs in hl and de the addresses in wTilemap
; for the treasure in Cell w2dffe
; hl = de + BG_MAP_WIDTH
.GetAttrmapAddress
	ld a, [w2dffe]
	ld d, HIGH(wTilemap) + $1
	cp NUM_COLLECTION_COLS
	jr nc, .got_hi
	ld d, HIGH(wTilemap)
.got_hi
	ld hl, .lo_map_addresses
	ld b, $00
	ld c, a
	add hl, bc
	ld l, [hl]
	ld h, d
	ld e, l
	ld a, BG_MAP_WIDTH
	add l
	ld l, a
	ret

.lo_map_addresses
	db $a3 ; 00
	db $a6 ; 01
	db $a9 ; 02
	db $ac ; 03
	db $af ; 04
	db $03 ; 05
	db $06 ; 06
	db $09 ; 07
	db $0c ; 08
	db $0f ; 09
	db $63 ; 10
	db $66 ; 11
	db $69 ; 12
	db $6c ; 13
	db $6f ; 14
	db $c3 ; 15
	db $c6 ; 16
	db $c9 ; 17
	db $cc ; 18
	db $cf ; 19

_TreasureCollection:
	call ProcessDPadRepeat
	call HandleTreasureCollectionInput
	call AnimateTreasureCollection
	call ClearUnusedVirtualOAM
	ret

HandleTreasureCollectionInput:
	ld a, [wJoypadPressed]
	bit B_BUTTON_F, a
	jr z, .asm_9a646

; b btn
	stop_music2
	stop_sfx
	ld a, $08
	ld [wSubState], a
	ret

.asm_9a646
	ld hl, wCollectionPage
	ld a, [wJoypadDown]
	ld b, a
	bit A_BUTTON_F, b
	jr z, .no_a_btn
	ld a, [wCollectionScrollMode]
	and a
	call z, .SetScrollMode
	ld hl, wCollectionPage
	bit D_RIGHT_F, b
	jr nz, .a_right
	bit D_LEFT_F, b
	jr nz, .a_left
	ret

.SetScrollMode
	ld a, TRUE
	ld [wCollectionScrollMode], a
	ld hl, wSceneObj9State
	ld a, $03
	call SetSceneObjState
	ld hl, wSceneObj10State
	ld a, $04
	call SetSceneObjState
	ret

.a_right
	ld a, $04
	cp [hl]
	jr z, .asm_9a682
	inc [hl]
	jr .asm_9a68f
.asm_9a682
	ld [hl], $00
	jr .asm_9a68f
.a_left
	ld a, [hl]
	and a
	jr z, .asm_9a68d
	dec [hl]
	jr .asm_9a68f
.asm_9a68d
	ld [hl], $04
.asm_9a68f
	play_sfx SFX_0EB
	stop_music2
	ld a, $0c
	ld [wSubState], a
	ret

.no_a_btn
	xor a
	ld [wCollectionScrollMode], a
	ld [wSceneObj9State], a
	ld [wSceneObj10State], a

	ld a, [wJoypadPressed]
	ld b, a
	ld hl, wCollectionCol
	ld a, [hl]
	bit D_RIGHT_F, b
	jr nz, .right
	bit D_LEFT_F, b
	jr nz, .left
	ld hl, wCollectionRow
	ld a, [hl]
	bit D_UP_F, b
	jr nz, .up
	bit D_DOWN_F, b
	jr nz, .down
	call ProcessCollectionLinkState
	ret

.right
	cp NUM_COLLECTION_COLS - 1
	jr z, .page_right
	inc [hl]
	ld a, [wCollectionCell]
	inc a
	jr .got_target_cell
.left
	and a
	jr z, .page_left
	dec [hl]
	ld a, [wCollectionCell]
	dec a
	jr .got_target_cell
.down
	cp NUM_COLLECTION_ROWS - 1
	jr nc, .cant_scroll
	inc [hl]
	ld a, [wCollectionCell]
	add NUM_COLLECTION_COLS
	jr .got_target_cell
.up
	and a
	jr z, .cant_scroll
	dec [hl]
	ld a, [wCollectionCell]
	sub NUM_COLLECTION_COLS
.got_target_cell
	ld [wCollectionCell], a
	play_sfx SFX_0E2

	xor a
	ld [wSceneObj2State], a
	ld [wSceneObj3State], a
	ld [wSceneObj4State], a
	ld [wSceneObj5State], a
	ld [wSceneObj6State], a
	ld [wSceneObj7State], a
	ld [wSceneObj8State], a

	ld de, wCollectionLinkState
	xor a
	ld [de], a ; wCollectionLinkState
	inc e
	ld [de], a ; wCollectionLinkStateCounter
	jr GetCollectionCellCoords

.cant_scroll
	play_sfx SFX_0E5
	ret

.page_right
	ld [hl], $00
	ld a, [wCollectionCell]
	sub NUM_COLLECTION_ROWS
	ld [wCollectionCell], a
	ld hl, wCollectionPage
	inc [hl]
	ld a, [hl]
	cp NUM_COLLECTION_PAGES
	jr nz, .change_page
	ld [hl], $00
	jr .change_page

.page_left
	ld [hl], NUM_COLLECTION_ROWS
	ld a, [wCollectionCell]
	add NUM_COLLECTION_ROWS
	ld [wCollectionCell], a
	ld hl, wCollectionPage
	dec [hl]
	ld a, [hl]
	cp -1
	jr nz, .change_page
	ld [hl], NUM_COLLECTION_PAGES - 1

.change_page
	play_sfx SFX_0EB
	ld a, $0c
	ld [wSubState], a
	ret

GetCollectionCellCoords:
	ld a, [wCollectionRow]
	ld hl, .y_coords
	ld c, a
	ld b, $00
	add hl, bc
	ld a, [hl]
	ld [wSceneObj1YCoord], a
	ld a, [wCollectionCol]
	ld hl, .x_coords
	ld c, a
	add hl, bc
	ld a, [hl]
	ld [wSceneObj1XCoord], a
	ret

.y_coords
	db $30 ; row 0
	db $48 ; row 1
	db $60 ; row 2
	db $78 ; row 3

.x_coords
	db $20 ; col 0
	db $38 ; col 1
	db $50 ; col 2
	db $68 ; col 3
	db $80 ; col 4

Func_9a78e:
	ld a, [de]
	ld hl, .table_1
	ld c, a
	ld b, $00
	add hl, bc
	ld a, [hl]
	ld [de], a
	inc e
	ld a, [de]
	ld hl, .table_2
	ld c, a
	add hl, bc
	ld a, [hl]
	ld [de], a
	ret

.table_1
	db $30 ; row 0
	db $48 ; row 1
	db $60 ; row 2
	db $78 ; row 3

.table_2
	db $20 ; col 0
	db $38 ; col 1
	db $50 ; col 2
	db $68 ; col 3
	db $80 ; col 4

GetCurrentCollectionCellRowAndColumn:
	ld a, [wCollectionCell]
	ld hl, wCollectionRow
;	fallthrough

GetCollectionCellRowAndColumn:
	ld b, 0
.loop_find_row
	cp NUM_COLLECTION_COLS
	jr c, .found_row
	sub NUM_COLLECTION_COLS
	inc b
	jr .loop_find_row
.found_row
	ld [hl], b ; row
	inc l
	ld [hli], a ; col
	ret

ProcessCollectionLinkState:
	ld hl, wCollectionLinkStateCounter
	inc [hl]
	ld b, h
	ld c, l

	ld a, [wCollectionLinkState]
	jumptable
	dw .AdvanceState
	dw .WaitDelay
	dw .GetLinkedCells
	dw .Return
	dw .Func_9a806
	dw .Return

.AdvanceState
	ld hl, wCollectionLinkStateCounter
	xor a
	ld [hld], a
	inc [hl] ; wCollectionLinkState
	ret

.WaitDelay
	ld a, [bc]
	cp $14
	ret c
	jr .AdvanceState

.GetLinkedCells
	ld hl, CollectionLinkTable
	ld a, [wCollectionPage]
	call GetPointerFromTableHL
	ld a, [wCollectionCell]
	call GetPointerFromTableHL
	ld a, [hl]
	cp $80
	jr z, .AdvanceState
	ld b, 7
	ld de, wCollectionLinkedCells
	call CopyHLToDE
	call .AdvanceState
	jp .AdvanceState

.Return
	ret

.Func_9a806
	ld a, [wCollectionLinkedCells + 0]
	cp $80
	jr z, .AdvanceState
	dec a
	ld hl, wCollectionCell
	cp [hl]
	jr z, .asm_9a823
	ld hl, wSceneObj2YCoord
	call GetCollectionCellRowAndColumn
	call .Func_9a8d8
	ld de, wSceneObj2YCoord
	call Func_9a78e
.asm_9a823
	ld a, [wCollectionLinkedCells + 1]
	cp $80
	jr z, .AdvanceState
	dec a
	ld hl, wCollectionCell
	cp [hl]
	jr z, .asm_9a840
	ld hl, wSceneObj3YCoord
	call GetCollectionCellRowAndColumn
	call .Func_9a8d8
	ld de, wSceneObj3YCoord
	call Func_9a78e
.asm_9a840
	ld a, [wCollectionLinkedCells + 2]
	cp $80
	jr z, .AdvanceState
	dec a
	ld hl, wCollectionCell
	cp [hl]
	jr z, .asm_9a85d
	ld hl, wSceneObj3End
	call GetCollectionCellRowAndColumn
	call .Func_9a8d8
	ld de, wSceneObj3End
	call Func_9a78e
.asm_9a85d
	ld a, [wCollectionLinkedCells + 3]
	cp $80
	jp z, .AdvanceState
	dec a
	ld hl, wCollectionCell
	cp [hl]
	jr z, .asm_9a87b
	ld hl, wSceneObj4End
	call GetCollectionCellRowAndColumn
	call .Func_9a8d8
	ld de, wSceneObj4End
	call Func_9a78e
.asm_9a87b
	ld a, [wCollectionLinkedCells + 4]
	cp $80
	jp z, .AdvanceState
	dec a
	ld hl, wCollectionCell
	cp [hl]
	jr z, .asm_9a899
	ld hl, wSceneObj5End
	call GetCollectionCellRowAndColumn
	call .Func_9a8d8
	ld de, wSceneObj5End
	call Func_9a78e
.asm_9a899
	ld a, [wCollectionLinkedCells + 5]
	cp $80
	jp z, .AdvanceState
	dec a
	ld hl, wCollectionCell
	cp [hl]
	jr z, .asm_9a8b7
	ld hl, wSceneObj6End
	call GetCollectionCellRowAndColumn
	call .Func_9a8d8
	ld de, wSceneObj6End
	call Func_9a78e
.asm_9a8b7
	ld a, [wCollectionLinkedCells + 6]
	cp $80
	jp z, .AdvanceState
	dec a
	ld hl, wCollectionCell
	cp [hl]
	jr z, .asm_9a8d5
	ld hl, wSceneObj7End
	call GetCollectionCellRowAndColumn
	call .Func_9a8d8
	ld de, wSceneObj7End
	call Func_9a78e
.asm_9a8d5
	jp .AdvanceState

.Func_9a8d8
	set 2, l
	ld a, $02
	call SetSceneObjState
	ret

CollectionLinkTable:
	dw .Page0
	dw .Page1
	dw .Page2
	dw .Page3
	dw .Page4

.none
	db $80

.Page0
	dw .music_boxes ; YELLOW_MUSIC_BOX
	dw .music_boxes ; BLUE_MUSIC_BOX
	dw .music_boxes ; GREEN_MUSIC_BOX
	dw .music_boxes ; RED_MUSIC_BOX
	dw .music_boxes ; GOLD_MUSIC_BOX
	dw .none ; PRINCE_FROGS_GLOVES_T
	dw .none ; SWIMMING_FLIPPERS_T
	dw .none ; HIGH_JUMP_BOOTS_T
	dw .none ; SUPER_GRAB_GLOVES_T
	dw .none ; GARLIC_T
	dw .none ; GRAB_GLOVE_T
	dw .none ; LEAD_OVERALLS_T
	dw .none ; SUPER_JUMP_SLAM_OVERALLS_T
	dw .none ; HEAD_SMASH_HELMET_T
	dw .lantern_and_flame ; LANTERN
	dw .lantern_and_flame ; MAGICAL_FLAME
	dw .none ; TORCH
	dw .gears ; GEAR_1
	dw .gears ; GEAR_2
	dw .none ; WARP_COMPACT

.music_boxes
	db $01, $02, $03, $04, $05, $80
.lantern_and_flame
	db $0f, $10, $80
.gears
	db $12, $13, $80

.Page1
	dw .none ; JAR
	dw .none ; TREASURE_MAP
	dw .book_and_trident ; BLUE_BOOK
	dw .none ; MAGIC_WAND
	dw .book_and_sky_key ; SKY_KEY
	dw .book_and_sky_key ; YELLOW_BOOK
	dw .none ; AXE
	dw .book_and_trident ; TRIDENT
	dw .skull_rings ; SKULL_RING_BLUE
	dw .skull_rings ; SKULL_RING_RED
	dw .tablets ; BLUE_TABLET
	dw .tablets ; GREEN_TABLET
	dw .none ; ORNAMENTAL_FAN
	dw .scroll ; TOP_HALF_OF_SCROLL
	dw .scroll ; BOTTOM_HALF_OF_SCROLL
	dw .tusks_and_flower ; TUSK_BLUE
	dw .tusks_and_flower ; TUSK_RED
	dw .tusks_and_flower ; GREEN_FLOWER
	dw .chemicals ; BLUE_CHEMICAL
	dw .chemicals ; RED_CHEMICAL

.book_and_trident
	db $03, $08, $80
.book_and_sky_key
	db $05, $06, $80
.skull_rings
	db $09, $0a, $80
.tablets
	db $0b, $0c, $80
.scroll
	db $0e, $0f, $80
.tusks_and_flower
	db $10, $11, $12, $80
.chemicals
	db $13, $14, $80

.Page2
	dw .none ; AIR_PUMP
	dw .none ; SAPLING_OF_GROWTH
	dw .none ; NIGHT_VISION_SCOPE
	dw .none ; ELECTRIC_FAN_PROPELLER
	dw .none ; RUST_SPRAY
	dw .none ; STATUE
	dw .none ; EXPLOSIVE_PLUNGER_BOX
	dw .none ; SCISSORS
	dw .none ; CASTLE_BRICK
	dw .none ; WARP_REMOVAL_APPARATUS
	dw .key_cards ; KEY_CARD_RED
	dw .key_cards ; KEY_CARD_BLUE
	dw .none ; JACKHAMMER
	dw .none ; PICK_AXE
	dw .none ; ROCKET
	dw .none ; POCKET_PET
	dw .none ; MYSTERY_HANDLE
	dw .none ; DEMONS_BLOOD
	dw .none ; GOLD_MAGIC
	dw .none ; FIGHTER_MANNEQUIN

.key_cards
	db $0b, $0c, $80

.Page3
	dw .none ; TRUCK_WHEEL
	dw .none ; FLUTE
	dw .none ; FOOT_OF_STONE
	dw .golden_eyes ; GOLDEN_RIGHT_EYE
	dw .golden_eyes ; GOLDEN_LEFT_EYE
	dw .glass_eyes ; RIGHT_GLASS_EYE
	dw .glass_eyes ; LEFT_GLASS_EYE
	dw .none ; SCEPTER
	dw .sun_medallion ; SUN_MEDALLION_TOP
	dw .sun_medallion ; SUN_MEDALLION_BOTTOM
	dw .storm_and_magic_seeds ; EYE_OF_THE_STORM
	dw .none ; POUCH
	dw .storm_and_magic_seeds ; MAGIC_SEEDS
	dw .none ; FULL_MOON_GONG
	dw .none ; TELEPHONE
	dw .none ; CROWN
	dw .none ; DAY_OR_NIGHT_SPELL
	dw .none ; RED_GEM
	dw .none ; GREEN_GEM
	dw .none ; BLUE_GEM

.golden_eyes
	db $04, $05, $80
.glass_eyes
	db $06, $07, $80
.sun_medallion
	db $09, $0a, $80
.storm_and_magic_seeds
	db $0b, $0d, $80

.Page4
	dw .none ; CLUBS_CREST
	dw .none ; SPADES_CREST
	dw .none ; HEART_CREST
	dw .none ; DIAMONDS_CREST
	dw .none ; EARTHEN_FIGURE
	dw .none ; SABER
	dw .none ; GOBLET
	dw .none ; TEAPOT
	dw .none ; MAGNIFYING_GLASS
	dw .none ; UFO
	dw .none ; MINICAR
	dw .none ; LOCOMOTIVE
	dw .none ; FIRE_DRENCHER
	dw .crayons ; CRAYON_RED_T
	dw .crayons ; CRAYON_BROWN_T
	dw .crayons ; CRAYON_YELLOW_T
	dw .crayons ; CRAYON_GREEN_T
	dw .crayons ; CRAYON_CYAN_T
	dw .crayons ; CRAYON_BLUE_T
	dw .crayons ; CRAYON_PINK_T

.crayons
	db $0e, $0f, $10, $11, $12, $13, $14, $80

Func_9a9ec:
	ld h, b
	ld l, c
	res 1, l
	ld b, $26
	call UpdateOWAnimation
	ld a, [bc]
	ret

Func_9a9f7:
	ld b, $26
	call AddOWSpriteWithScroll
	ret

Func_9a9fd:
	ld a, [wOWAnimationFinished]
	cp $ff
	ret nz
	set 2, l
	xor a
	ld [hli], a
	ld [hli], a
	ld a, e
	ld [hl], a
	ld b, h
	ld c, l
	ret

AnimateTreasureCollection:
	ld bc, wSceneObj1State
	call .Func_9aa4a
	ld bc, wSceneObj2State
	call .Func_9aa4a
	ld bc, wSceneObj3State
	call .Func_9aa4a
	ld bc, wSceneObj4State
	call .Func_9aa4a
	ld bc, wSceneObj5State
	call .Func_9aa4a
	ld bc, wSceneObj6State
	call .Func_9aa4a
	ld bc, wSceneObj7State
	call .Func_9aa4a
	ld bc, wSceneObj8State
	call .Func_9aa4a
	ld bc, wSceneObj9State
	call .Func_9aa4a
	ld bc, wSceneObj10State
	call .Func_9aa4a
	ret

.Func_9aa4a
	ld a, [bc]
	jumptable

	dw Func_9aa6c
	dw Func_9aa6d
	dw Func_9aa72
	dw Func_9aa77
	dw Func_9aa7c

Func_9aa56:
	call Func_9a9ec
Func_9aa59:
	ld de, OAM_9ac5d
	call Func_9a9f7
	ret

; unreferenced
Func_9aa60:
	call Func_9a9ec
	ld e, $00
	call Func_9a9fd
	jr nz, Func_9aa59
	jr AnimateTreasureCollection.Func_9aa4a

Func_9aa6c:
	ret

Func_9aa6d:
	ld de, Frameset_9ace1
	jr Func_9aa56

Func_9aa72:
	ld de, Frameset_9acea
	jr Func_9aa56

Func_9aa77:
	ld de, Frameset_9acf3
	jr Func_9aa56

Func_9aa7c:
	ld de, Frameset_9acfc
	jr Func_9aa56

; input:
; c = * constant
GetLevelThatContainsTreasure:
	ld b, -1
	ld hl, LevelTreasureIDs
.loop
	inc b
	ld a, [hli]
	cp c
	jr nz, .loop
	srl b
	srl b ; /4
	ret

LoadLevelTreasureData:
	ldh a, [rSVBK]
	push af
	ld a, BANK("WRAM2")
	ldh [rSVBK], a
	call ClearTreasureData
	call LoadLevelTreasures
	call LoadTreasurePals
	call LoadTreasureTiles

	xor a ; VRAM0
	ldh [rVBK], a
	ld hl, wTileBuffer
	ld de, v0Tiles1 tile $10
	ld b, $00 ; $100 bytes = 10 tiles
	call CopyHLToDE
	pop af
	ldh [rSVBK], a
	ret

Func_9aab5:
	ld a, [wMapSideLevelID]
	cp OW_EXITS
	ret nc

	call ClearTreasureData
	call Func_9aaf4
	call Func_9ab1c

	ld a, [wGameModeFlags]
	bit MODE_TIME_ATTACK_F, a
	jp nz, GetLevelTimeAttackScore

	call LoadLevelTreasures
	call FillBottomBarTreasureIDs
	call WriteUncollectedBottomBarTreasureIDs
	call LoadTreasurePals
	call LoadTreasureTiles

	ld a, HIGH(wTileBuffer)
	ld [wHDMASourceHi], a
	ld a, LOW(wTileBuffer)
	ld [wHDMASourceLo], a
	ld a, $09
	ld [wHDMADestHi], a
	xor a
	ld [wHDMADestLo], a
	ld a, hdma 16
	ld [wHDMAMode], a
	ret

Func_9aaf4:
	ld a, [wOWLevel]
	call CheckLevelMusicalCoinFlag
	ret z ; didn't get all musical coins
	ld hl, wBottomBarFlags
	set 6, [hl]
	ld hl, wCompassObjState
	xor a
	ld [hli], a
	ld [hl], a ; wCompassObjUnk7
	ret

; clears treasure tiles and info in WRAM
ClearTreasureData:
; clear the treasure tiles
	xor a
	ld hl, wTileBuffer
	ld bc, 16 tiles
	call WriteAToHL_BCTimes

; clear the treasure info
	xor a
	ld hl, wLevelTreasureIDs
	ld bc, $10
	call WriteAToHL_BCTimes
	ret

Func_9ab1c:
	ld a, [wOWLevel]
	and a
	ret z
	cp $80
	ret z
	farcall GetOWLevelCollectedTreasures
	ret

LoadTreasureTiles:
	ld a, [wLevelGreyTreasureID]
	ld de, wTileBuffer
	call .CopyGfx
	ld a, [wLevelRedTreasureID]
	call .CopyGfx
	ld a, [wLevelGreenTreasureID]
	call .CopyGfx
	ld a, [wLevelBlueTreasureID]
	call .CopyGfx
	; bug, fallthrough

.CopyGfx
; this gets right addr given that a < 128
	and a
	ld b, $00
	rla
	rla
	rl b
	ld c, a
	and $f0
	or b
	swap a
	ld b, a
	ld a, $0f
	and c
	swap a
	ld c, a
	; bc = a * 4 tiles
	ld hl, TreasureGfx
	add hl, bc
	ld b, 4 tiles
	call CopyHLToDE
	ret

; for each treasure that hasn't been collected
; in the current level, write in [de]
; a value of $00 (TREASURE_NONE)
WriteUncollectedBottomBarTreasureIDs:
	ld a, [wKeyAndTreasureFlags]
	swap a
	and $0f
	ld b, a
	ld c, NUM_LEVEL_TREASURES
	ld de, wLevelTreasureIDs
.loop_level_treasures
	srl b
	jr c, .next
	xor a ; TREASURE_NONE
	ld [de], a
.next
	inc e
	dec c
	jr nz, .loop_level_treasures
	ret

; loads in wLevelTreasureIDs the treasure IDs for level in wOWLevel
LoadLevelTreasures:
	ld hl, LevelTreasureIDs
	ld de, wLevelTreasureIDs
	call .GetLevelTreasures
	ret ; can be fallthrough

.GetLevelTreasures
	ld a, [wOWLevel]
	add a
	add a ; *4
	ld c, a
	ld b, $00
	add hl, bc
	ld b, NUM_LEVEL_TREASURES
.loop
	ld a, [hli]
	ld [de], a
	inc e
	dec b
	jr nz, .loop
	ret

LoadTreasurePals:
	ld de, TreasureOBPals
	ld a, [wLevelGreyTreasureID]
	ld l, a
	ld h, $00
	add hl, de
	ld a, [hl]
	ld [wLevelGreyTreasurePal], a

	ld a, [wLevelRedTreasureID]
	ld l, a
	ld h, $00
	add hl, de
	ld a, [hl]
	ld [wLevelRedTreasurePal], a

	ld a, [wLevelGreenTreasureID]
	ld l, a
	ld h, $00
	add hl, de
	ld a, [hl]
	ld [wLevelGreenTreasurePal], a

	ld a, [wLevelBlueTreasureID]
	ld l, a
	ld h, $00
	add hl, de
	ld a, [hl]
	ld [wLevelBlueTreasurePal], a
	ret

GetLevelTimeAttackScore:
	ld a, [wOWLevel]
	and a
	ret z
	cp $80
	ret z
	ld hl, wLevelTimeAttackScores
	dec a
	ld de, w2d0ee
	add a ; *2
	ld c, a
	ld b, $00
	add hl, bc
	ld a, [hli]
	ld [de], a ; w2d0ee
	inc e
	ld a, [hl]
	ld [de], a ; w2d0ef
	ret

; fills wBottomBarTreasureIDs
; with treasures in wLevelTreasureIDs
FillBottomBarTreasureIDs:
	ld de, wBottomBarTreasureIDs
	ld hl, wLevelTreasureIDs
	ld b, NUM_LEVEL_TREASURES
	call CopyHLToDE
	ret

TreasureOBPals:
	db OBPAL_TREASURE_YELLOW ; TREASURE_NONE
	db OBPAL_TREASURE_YELLOW ; YELLOW_MUSIC_BOX
	db OBPAL_TREASURE_BLUE   ; BLUE_MUSIC_BOX
	db OBPAL_TREASURE_GREEN  ; GREEN_MUSIC_BOX
	db OBPAL_TREASURE_RED    ; RED_MUSIC_BOX
	db OBPAL_TREASURE_YELLOW ; GOLD_MUSIC_BOX
	db OBPAL_TREASURE_GREEN  ; PRINCE_FROGS_GLOVES_T
	db OBPAL_TREASURE_GREEN  ; SWIMMING_FLIPPERS_T
	db OBPAL_TREASURE_BLUE   ; HIGH_JUMP_BOOTS_T
	db OBPAL_TREASURE_YELLOW ; SUPER_GRAB_GLOVES_T
	db OBPAL_TREASURE_YELLOW ; GARLIC_T
	db OBPAL_TREASURE_RED    ; GRAB_GLOVE_T
	db OBPAL_TREASURE_BLUE   ; LEAD_OVERALLS_T
	db OBPAL_TREASURE_RED    ; SUPER_JUMP_SLAM_OVERALLS_T
	db OBPAL_TREASURE_GREEN  ; HEAD_SMASH_HELMET_T
	db OBPAL_TREASURE_YELLOW ; LANTERN
	db OBPAL_TREASURE_RED    ; MAGICAL_FLAME
	db OBPAL_TREASURE_RED    ; TORCH
	db OBPAL_TREASURE_BLUE   ; GEAR_1
	db OBPAL_TREASURE_BLUE   ; GEAR_2
	db OBPAL_TREASURE_BLUE   ; WARP_COMPACT
	db OBPAL_TREASURE_BLUE   ; JAR
	db OBPAL_TREASURE_YELLOW ; TREASURE_MAP
	db OBPAL_TREASURE_BLUE   ; BLUE_BOOK
	db OBPAL_TREASURE_YELLOW ; MAGIC_WAND
	db OBPAL_TREASURE_YELLOW ; SKY_KEY
	db OBPAL_TREASURE_GREEN  ; YELLOW_BOOK
	db OBPAL_TREASURE_BLUE   ; AXE
	db OBPAL_TREASURE_YELLOW ; TRIDENT
	db OBPAL_TREASURE_BLUE   ; SKULL_RING_BLUE
	db OBPAL_TREASURE_RED    ; SKULL_RING_RED
	db OBPAL_TREASURE_BLUE   ; BLUE_TABLET
	db OBPAL_TREASURE_GREEN  ; GREEN_TABLET
	db OBPAL_TREASURE_RED    ; ORNAMENTAL_FAN
	db OBPAL_TREASURE_YELLOW ; TOP_HALF_OF_SCROLL
	db OBPAL_TREASURE_YELLOW ; BOTTOM_HALF_OF_SCROL
	db OBPAL_TREASURE_GREEN  ; TUSK_BLUE
	db OBPAL_TREASURE_GREEN  ; TUSK_RED
	db OBPAL_TREASURE_GREEN  ; GREEN_FLOWER
	db OBPAL_TREASURE_BLUE   ; BLUE_CHEMICAL
	db OBPAL_TREASURE_RED    ; RED_CHEMICAL
	db OBPAL_TREASURE_YELLOW ; AIR_PUMP
	db OBPAL_TREASURE_YELLOW ; SAPLING_OF_GROWTH
	db OBPAL_TREASURE_GREEN  ; NIGHT_VISION_SCOPE
	db OBPAL_TREASURE_BLUE   ; ELECTRIC_FAN_PROPELLER
	db OBPAL_TREASURE_RED    ; RUST_SPRAY
	db OBPAL_TREASURE_RED    ; STATUE
	db OBPAL_TREASURE_BLUE   ; EXPLOSIVE_PLUNGER_BOX
	db OBPAL_TREASURE_BLUE   ; SCISSORS
	db OBPAL_TREASURE_RED    ; CASTLE_BRICK
	db OBPAL_TREASURE_GREEN  ; WARP_REMOVAL_APPARATUS
	db OBPAL_TREASURE_RED    ; KEY_CARD_RED
	db OBPAL_TREASURE_BLUE   ; KEY_CARD_BLUE
	db OBPAL_TREASURE_GREEN  ; JACKHAMMER
	db OBPAL_TREASURE_YELLOW ; PICK_AXE
	db OBPAL_TREASURE_YELLOW ; ROCKET
	db OBPAL_TREASURE_YELLOW ; POCKET_PET
	db OBPAL_TREASURE_BLUE   ; MYSTERY_HANDLE
	db OBPAL_TREASURE_RED    ; DEMONS_BLOOD
	db OBPAL_TREASURE_YELLOW ; GOLD_MAGIC
	db OBPAL_TREASURE_BLUE   ; FIGHTER_MANNEQUIN
	db OBPAL_TREASURE_BLUE   ; TRUCK_WHEEL
	db OBPAL_TREASURE_YELLOW ; FLUTE
	db OBPAL_TREASURE_GREEN  ; FOOT_OF_STONE
	db OBPAL_TREASURE_YELLOW ; GOLDEN_RIGHT_EYE
	db OBPAL_TREASURE_YELLOW ; GOLDEN_LEFT_EYE
	db OBPAL_TREASURE_BLUE   ; RIGHT_GLASS_EYE
	db OBPAL_TREASURE_BLUE   ; LEFT_GLASS_EYE
	db OBPAL_TREASURE_BLUE   ; SCEPTER
	db OBPAL_TREASURE_RED    ; SUN_MEDALLION_TOP
	db OBPAL_TREASURE_RED    ; SUN_MEDALLION_BOTTOM
	db OBPAL_TREASURE_BLUE   ; EYE_OF_THE_STORM
	db OBPAL_TREASURE_GREEN  ; POUCH
	db OBPAL_TREASURE_BLUE   ; MAGIC_SEEDS
	db OBPAL_TREASURE_YELLOW ; FULL_MOON_GONG
	db OBPAL_TREASURE_GREEN  ; TELEPHONE
	db OBPAL_TREASURE_RED    ; CROWN
	db OBPAL_TREASURE_RED    ; DAY_OR_NIGHT_SPELL
	db OBPAL_TREASURE_RED    ; RED_GEM
	db OBPAL_TREASURE_GREEN  ; GREEN_GEM
	db OBPAL_TREASURE_BLUE   ; BLUE_GEM
	db OBPAL_TREASURE_GREEN  ; CLUBS_CREST
	db OBPAL_TREASURE_YELLOW ; SPADES_CREST
	db OBPAL_TREASURE_RED    ; HEART_CREST
	db OBPAL_TREASURE_BLUE   ; DIAMONDS_CREST
	db OBPAL_TREASURE_YELLOW ; EARTHEN_FIGURE
	db OBPAL_TREASURE_BLUE   ; SABER
	db OBPAL_TREASURE_BLUE   ; GOBLET
	db OBPAL_TREASURE_YELLOW ; TEAPOT
	db OBPAL_TREASURE_BLUE   ; MAGNIFYING_GLASS
	db OBPAL_TREASURE_BLUE   ; UFO
	db OBPAL_TREASURE_RED    ; MINICAR
	db OBPAL_TREASURE_YELLOW ; LOCOMOTIVE
	db OBPAL_TREASURE_RED    ; FIRE_DRENCHER
	db OBPAL_TREASURE_RED    ; CRAYON_RED_T
	db OBPAL_TREASURE_YELLOW ; CRAYON_BROWN_T
	db OBPAL_TREASURE_YELLOW ; CRAYON_YELLOW_T
	db OBPAL_TREASURE_GREEN  ; CRAYON_GREEN_T
	db OBPAL_TREASURE_BLUE   ; CRAYON_CYAN_T
	db OBPAL_TREASURE_BLUE   ; CRAYON_BLUE_T
	db OBPAL_TREASURE_RED    ; CRAYON_PINK_T
	db $03 ; 101

OAM_9ac5d:
	dw .frame_0
	dw .frame_1
	dw .frame_2
	dw .frame_3
	dw .frame_4
	dw .frame_5
	dw .frame_6
	dw .frame_7
	dw .frame_8
	dw .frame_9
	dw .frame_10
	dw .frame_11

.frame_0
	frame_oam -23, -11, $00, 0
	frame_oam  -7, -11, $02, 0
	frame_oam -23,   3, $00, 0 | OAMF_XFLIP
	frame_oam  -7,   3, $02, 0 | OAMF_XFLIP
	db $80

.frame_1
	frame_oam -22, -11, $04, 0
	frame_oam  -6, -11, $06, 0
	frame_oam -22,   3, $04, 0 | OAMF_XFLIP
	frame_oam  -6,   3, $06, 0 | OAMF_XFLIP
	db $80

.frame_2
	frame_oam -21, -11, $08, 0
	frame_oam  -5, -11, $0a, 0
	frame_oam -21,   3, $08, 0 | OAMF_XFLIP
	frame_oam  -5,   3, $0a, 0 | OAMF_XFLIP
	db $80

.frame_3
	frame_oam  -8, -11, $0c, 0
	frame_oam  -8,   3, $0c, 0 | OAMF_XFLIP
	db $80

.frame_4
	frame_oam  -8, -11, $0e, 0
	frame_oam  -8,   3, $0e, 0 | OAMF_XFLIP
	db $80

.frame_5
	frame_oam  -8, -11, $10, 1
	frame_oam  -8,   3, $10, 1 | OAMF_XFLIP
	db $80

.frame_6
	frame_oam  -8,  -4, $12, 2
	db $80

.frame_7
	frame_oam  -8,  -4, $14, 2
	db $80

.frame_8
	frame_oam  -8,  -4, $16, 2
	db $80

.frame_9
	frame_oam  -8,  -4, $12, 2 | OAMF_XFLIP
	db $80

.frame_10
	frame_oam  -8,  -4, $14, 2 | OAMF_XFLIP
	db $80

.frame_11
	frame_oam  -8,  -4, $16, 2 | OAMF_XFLIP
	db $80

Frameset_9ace1::
	db $00,  3
	db $01,  3
	db $02,  3
	db $01,  3
	db $ff

Frameset_9acea::
	db $03,  6
	db $04,  6
	db $05,  6
	db $04,  6
	db $ff

Frameset_9acf3::
	db $06,  8
	db $07,  4
	db $08,  8
	db $07,  4
	db $ff

Frameset_9acfc::
	db $09,  8
	db $0a,  4
	db $0b,  8
	db $0a,  4
	db $ff

Pals_9ad05:
	rgb 31, 31, 31
	rgb 10, 31, 10
	rgb  0, 20,  0
	rgb  0,  0,  0

	rgb 31, 31, 31
	rgb 10, 31, 10
	rgb 20, 31, 20
	rgb  0,  0,  7

	rgb 31, 31, 31
	rgb 15, 25, 31
	rgb  0, 15, 31
	rgb  0,  0,  0

	rgb 31, 31, 31
	rgb 25, 25, 25
	rgb 15, 15, 15
	rgb  0,  0,  7

	rgb 31, 31, 31
	rgb 25, 25, 25
	rgb 15, 15, 15
	rgb  0,  0,  7

	rgb 31, 31, 31
	rgb 25, 25, 25
	rgb 15, 15, 15
	rgb  0,  0,  7

	rgb 31, 31, 31
	rgb 25, 25, 25
	rgb 15, 15, 15
	rgb  0,  0,  7

	rgb 31, 31, 31
	rgb 25, 25, 25
	rgb 15, 15, 15
	rgb  0,  0,  7

Pals_9ad45:
	rgb 31, 31, 31
	rgb 20, 31, 31
	rgb 10, 20, 31
	rgb  0,  0,  0

	rgb 31, 31, 31
	rgb 31, 31, 31
	rgb 31, 10,  0
	rgb  0,  0,  0

	rgb 31, 31, 31
	rgb 20,  0,  0
	rgb 31, 15,  0
	rgb  0,  0,  0

	rgb 31, 31, 31
	rgb 31, 31,  0
	rgb 31,  4, 13
	rgb  0,  0,  0

TreasurePals::
TreasureYellowPal:
; OBPAL_TREASURE_YELLOW
	rgb 31, 31, 31
	rgb 31, 31,  0
	rgb 19, 16,  0
	rgb  5,  4,  0

TreasureRedPal:
; OBPAL_TREASURE_RED
	rgb 31, 31, 31
	rgb 31, 19, 23
	rgb 27,  0,  6
	rgb  7,  0,  0

TreasureGreenPal:
; OBPAL_TREASURE_GREEN
	rgb 31, 31, 31
	rgb 22, 31, 20
	rgb  0, 21,  0
	rgb  0,  2,  0

TreasureBluePal:
; OBPAL_TREASURE_BLUE
	rgb 31, 31, 31
	rgb  2, 31, 31
	rgb  6,  6, 31
	rgb  0,  0,  7
