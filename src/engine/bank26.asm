TreasureGfx:
INCBIN "gfx/treasures/treasure_000.2bpp"
INCBIN "gfx/treasures/yellow_music_box.2bpp"
INCBIN "gfx/treasures/blue_music_box.2bpp"
INCBIN "gfx/treasures/green_music_box.2bpp"
INCBIN "gfx/treasures/red_music_box.2bpp"
INCBIN "gfx/treasures/gold_music_box.2bpp"
INCBIN "gfx/treasures/prince_frogs_gloves.2bpp"
INCBIN "gfx/treasures/swimming_flippers.2bpp"
INCBIN "gfx/treasures/high_jump_boots.2bpp"
INCBIN "gfx/treasures/super_grab_gloves.2bpp"
INCBIN "gfx/treasures/garlic.2bpp"
INCBIN "gfx/treasures/grab_glove.2bpp"
INCBIN "gfx/treasures/lead_overalls.2bpp"
INCBIN "gfx/treasures/super_jump_slam_overalls.2bpp"
INCBIN "gfx/treasures/head_smash_helmet.2bpp"
INCBIN "gfx/treasures/lantern.2bpp"
INCBIN "gfx/treasures/magical_flame.2bpp"
INCBIN "gfx/treasures/torch.2bpp"
INCBIN "gfx/treasures/gear_1.2bpp"
INCBIN "gfx/treasures/gear_2.2bpp"
INCBIN "gfx/treasures/warp_compact.2bpp"
INCBIN "gfx/treasures/jar.2bpp"
INCBIN "gfx/treasures/treasure_map.2bpp"
INCBIN "gfx/treasures/blue_book.2bpp"
INCBIN "gfx/treasures/sky_key.2bpp"
INCBIN "gfx/treasures/yellow_book.2bpp"
INCBIN "gfx/treasures/trident.2bpp"
INCBIN "gfx/treasures/axe.2bpp"
INCBIN "gfx/treasures/magic_wand.2bpp"
INCBIN "gfx/treasures/skull_ring_blue.2bpp"
INCBIN "gfx/treasures/skull_ring_red.2bpp"
INCBIN "gfx/treasures/blue_tablet.2bpp"
INCBIN "gfx/treasures/green_tablet.2bpp"
INCBIN "gfx/treasures/ornamental_fan.2bpp"
INCBIN "gfx/treasures/top_half_of_scroll.2bpp"
INCBIN "gfx/treasures/bottom_half_of_scroll.2bpp"
INCBIN "gfx/treasures/tusk_blue.2bpp"
INCBIN "gfx/treasures/tusk_red.2bpp"
INCBIN "gfx/treasures/green_flower.2bpp"
INCBIN "gfx/treasures/blue_chemical.2bpp"
INCBIN "gfx/treasures/red_chemical.2bpp"
INCBIN "gfx/treasures/air_pump.2bpp"
INCBIN "gfx/treasures/sapling_of_growth.2bpp"
INCBIN "gfx/treasures/night_vision_scope.2bpp"
INCBIN "gfx/treasures/electric_fan_propeller.2bpp"
INCBIN "gfx/treasures/rust_spray.2bpp"
INCBIN "gfx/treasures/statue.2bpp"
INCBIN "gfx/treasures/explosive_plunger_box.2bpp"
INCBIN "gfx/treasures/scissors.2bpp"
INCBIN "gfx/treasures/castle_brick.2bpp"
INCBIN "gfx/treasures/warp_removal_apparatus.2bpp"
INCBIN "gfx/treasures/key_card_red.2bpp"
INCBIN "gfx/treasures/key_card_blue.2bpp"
INCBIN "gfx/treasures/jackhammer.2bpp"
INCBIN "gfx/treasures/pick_axe.2bpp"
INCBIN "gfx/treasures/rocket.2bpp"
INCBIN "gfx/treasures/pocket_pet.2bpp"
INCBIN "gfx/treasures/mystery_handle.2bpp"
INCBIN "gfx/treasures/demons_blood.2bpp"
INCBIN "gfx/treasures/gold_magic.2bpp"
INCBIN "gfx/treasures/fighter_mannequin.2bpp"
INCBIN "gfx/treasures/truck_wheel.2bpp"
INCBIN "gfx/treasures/flute.2bpp"
INCBIN "gfx/treasures/foot_of_stone.2bpp"
INCBIN "gfx/treasures/golden_right_eye.2bpp"
INCBIN "gfx/treasures/golden_left_eye.2bpp"
INCBIN "gfx/treasures/right_glass_eye.2bpp"
INCBIN "gfx/treasures/left_glass_eye.2bpp"
INCBIN "gfx/treasures/scepter.2bpp"
INCBIN "gfx/treasures/sun_medallion_top.2bpp"
INCBIN "gfx/treasures/sun_medallion_bottom.2bpp"
INCBIN "gfx/treasures/eye_of_the_storm.2bpp"
INCBIN "gfx/treasures/magic_seeds.2bpp"
INCBIN "gfx/treasures/pouch.2bpp"
INCBIN "gfx/treasures/full_moon_gong.2bpp"
INCBIN "gfx/treasures/telephone.2bpp"
INCBIN "gfx/treasures/crown.2bpp"
INCBIN "gfx/treasures/day_or_night_spell.2bpp"
INCBIN "gfx/treasures/red_gem.2bpp"
INCBIN "gfx/treasures/green_gem.2bpp"
INCBIN "gfx/treasures/blue_gem.2bpp"
INCBIN "gfx/treasures/clubs_crest.2bpp"
INCBIN "gfx/treasures/spades_crest.2bpp"
INCBIN "gfx/treasures/heart_crest.2bpp"
INCBIN "gfx/treasures/diamonds_crest.2bpp"
INCBIN "gfx/treasures/earthen_figure.2bpp"
INCBIN "gfx/treasures/saber.2bpp"
INCBIN "gfx/treasures/goblet.2bpp"
INCBIN "gfx/treasures/teapot.2bpp"
INCBIN "gfx/treasures/magnifying_glass.2bpp"
INCBIN "gfx/treasures/ufo.2bpp"
INCBIN "gfx/treasures/minicar.2bpp"
INCBIN "gfx/treasures/locomotive.2bpp"
INCBIN "gfx/treasures/fire_drencher.2bpp"
INCBIN "gfx/treasures/crayon_red.2bpp"
INCBIN "gfx/treasures/crayon_brown.2bpp"
INCBIN "gfx/treasures/crayon_yellow.2bpp"
INCBIN "gfx/treasures/crayon_green.2bpp"
INCBIN "gfx/treasures/crayon_cyan.2bpp"
INCBIN "gfx/treasures/crayon_blue.2bpp"
INCBIN "gfx/treasures/crayon_pink.2bpp"

Treasure101Gfx:
INCBIN "gfx/treasures/treasure_101.2bpp"

NumbersGfx:
INCBIN "gfx/gui/numbers.2bpp"

TreasureCollectionGfx:
INCBIN "gfx/gui/treasure_collection.2bpp.rle"

TreasureCollectionCursorGfx:
INCBIN "gfx/gui/treasure_collection_cursor.2bpp.rle"

BGMap_9a129:
INCBIN "gfx/bgmaps/map_9a129.bin.rle"

BGMap_9a2ac:
INCBIN "gfx/bgmaps/map_9a2ac.bin.rle"

DrawCoinCount:
	ld de, v0Tiles1 tile $a
	ld a, [wNumCoins + 0]
	call .DrawNumber
	ld a, [wNumCoins + 1]
	swap a
	call .DrawNumber
	ld a, [wNumCoins + 1]
	call .DrawNumber
	ret

.DrawNumber:
	and $0f
	inc a
	ld hl, NumbersGfx
	ld bc, $20
.loop
	dec a
	jr z, .got_number
	add hl, bc
	jr .loop
.got_number
	ld b, $20
	call CopyHLToDE
	ret

_InitTreasureCollection:
	call DisableLCD
	call FillBGMap0_With7f
	call ClearVirtualOAM

	play_music2 MUSIC_TREASURE_COLLECTION

	xor a
	ldh [rSCX], a
	ld [wSCX], a
	ldh [rSCY], a
	ld [wSCY], a

	ld a, $02
	ldh [rSVBK], a
	call VBlank_TreasureCollection
	call Func_9a54e
	call Func_9a559
	xor a
	ld [wHDMA], a

	; if collection is already open,
	; then we don't need to search for
	; the right page
	ld a, [wIsCollectionOpen]
	and a
	jr nz, .skip_jump_page

	lb bc, 0, 0
	ld a, [wTransitionParam]
	and a
	jr z, .got_page
	cp NUM_TREASURES
	jr nc, .got_page
.loop_find_page
	cp NUM_COLLECTION_CELLS + 1
	jr c, .found_page
	inc b
	sub NUM_COLLECTION_CELLS
	jr .loop_find_page
.found_page
	dec a
	ld c, a
.got_page
	ld a, b
	ld [wCollectionPage], a
	ld a, c
	ld [wCollectionCell], a
	ld a, TRUE
	ld [wIsCollectionOpen], a

.skip_jump_page
	call GetCurrentCollectionCellRowAndColumn
	call GetCollectionCellCoords

	ld hl, Pals_9ad45
	call LoadPalsToTempPals1
	ld hl, Pals_9ad05
	call LoadPalsToTempPals2

	decompress_tiles0 TreasureCollectionCursorGfx, v0Tiles0
	decompress_tiles0 TreasureCollectionGfx, v0Tiles2

	call LoadCollectionPageTreasureGfx

	ld a, BANK("VRAM1")
	ldh [rVBK], a
	ld hl, NumbersGfx
	ld de, v1Tiles1 tile $20
	ld bc, $14 tiles
	call CopyHLToDE_BC
	xor a
	ldh [rVBK], a

	decompress_bgmap0 BGMap_9a2ac, wAttrmap
	decompress_bgmap0 BGMap_9a129, wTilemap

	call ApplyPageTreasuresPals

	hlbgcoord 2, 1, wAttrmap
	ld a, $1 | BGF_BANK1
	ld [hli], a
	ld [hli], a
	ld [hl], a
	hlbgcoord 2, 2, wAttrmap
	ld [hli], a
	ld [hli], a
	ld [hl], a

	ld a, [wCollectionPage]
	inc a
	add $20
	ldcoord_a 16, 1, wTilemap

	ld hl, wTilemap
	ld de, v0BGMap1
	ld bc, 18 * BG_MAP_WIDTH
	call CopyHLToDE_BC
	ld a, BANK("VRAM1")
	ldh [rVBK], a
	ld hl, wAttrmap
	ld de, v0BGMap1
	ld bc, 18 * BG_MAP_WIDTH
	call CopyHLToDE_BC

	xor a
	ldh [rVBK], a
	hlbgcoord 2, 1, v0BGMap1
	farcall PrintNumCollectedTreasures

	ld hl, wSceneObj1State
	ld a, $01
	call SetSceneObjState
	ld b, $10
	ld a, $08
	ld hl, wSceneObj9XCoord
	ld [hld], a
	ld [hl], b
	ld hl, wSceneObj10XCoord
	ld a, $98
	ld [hld], a
	ld [hl], b
	ld a, LCDCF_ON | LCDCF_BG9C00 | LCDCF_OBJ16 | LCDCF_OBJON | LCDCF_BGON
	ldh [rLCDC], a
	ld hl, wSubState
	inc [hl]
	ret

LoadCollectionPageTreasureGfx:
	ld hl, (TreasureGfx + 4 tiles) - (NUM_TREASURE_TILES * NUM_COLLECTION_CELLS) tiles
	ld de, (NUM_TREASURE_TILES * NUM_COLLECTION_CELLS) tiles
	ld a, [wCollectionPage]
	inc a
.loop
	add hl, de
	dec a
	jr nz, .loop
	ld de, v0Tiles1 tile $20
	ld bc, (NUM_TREASURE_TILES * NUM_COLLECTION_CELLS) tiles
	call CopyHLToDE_BC

	ld hl, Treasure101Gfx
	ld de, v0Tiles1 tile $7c
	ld b, NUM_TREASURE_TILES tiles
	call CopyHLToDE
	ret

Func_9a54e:
	ld hl, wCollectionRow
	ld bc, $800
	xor a
	call WriteAToHL_BCTimes
	ret

Func_9a559:
	ld hl, wCurSceneObj
	ld bc, $200
	xor a
	call WriteAToHL_BCTimes
	ret

VBlank_TreasureCollection:
	ld hl, .Func
	ld de, wVBlankFunc
	ld b, .end - .Func
	call CopyHLToDE
	ret

.Func:
	ld a, $02
	ldh [rSVBK], a
	ld a, [wSCY]
	ldh [rSCY], a
	ld a, [wSCX]
	ldh [rSCX], a
	xor a
	ldh [rVBK], a
	ld a, HIGH(wVirtualOAM)
	jp hTransferVirtualOAM
.end

; iterates all treasures in the current page
; and applies the corresponding palette
ApplyPageTreasuresPals:
	xor a
	ld [w2dffe], a
	ld a, [wCollectionPage]
	ld c, a
	inc c
	ld a, -NUM_COLLECTION_CELLS
.loop
	add NUM_COLLECTION_CELLS
	dec c
	jr nz, .loop
	inc a
	ld [w2dfff], a
.loop_treasures
	ld a, [w2dfff]
	call IsTreasureCollected
	jr z, .not_collected
	jr .collected

.next_treasure
	ld hl, w2dffe
	inc [hl]
	ld a, [hli]
	inc [hl] ; w2dfff
	cp NUM_COLLECTION_CELLS
	jr nz, .loop_treasures
	ret

.not_collected
	call .GetAttrmapAddress
	push de
	ld a, $fc
	ld [de], a
	inc a
	ld [hli], a
	inc a
	inc e
	ld [de], a
	inc a
	ld [hl], a
	pop hl
	dec h
	dec h
	dec h
	ld a, $04
	ld [hli], a
	ld [hl], a
	ld de, BG_MAP_WIDTH
	add hl, de
	ld [hld], a
	ld [hl], a
	jr .next_treasure

.collected
	ld a, [w2dfff]
	ld e, a
	ld d, $00
	ld hl, TreasureOBPals
	add hl, de
	ld a, [hl]
	push af
	call .GetAttrmapAddress
	pop af
	dec h
	dec h
	dec h
	ld [hli], a
	ld [hl], a
	dec d
	dec d
	dec d
	ld [de], a
	inc de
	ld [de], a
	jr .next_treasure

; outputs in hl and de the addresses in wTilemap
; for the treasure in Cell w2dffe
; hl = de + BG_MAP_WIDTH
.GetAttrmapAddress:
	ld a, [w2dffe]
	ld d, HIGH(wTilemap) + $1
	cp NUM_COLLECTION_COLS
	jr nc, .got_hi
	ld d, HIGH(wTilemap)
.got_hi
	ld hl, .lo_map_addresses
	ld b, $00
	ld c, a
	add hl, bc
	ld l, [hl]
	ld h, d
	ld e, l
	ld a, BG_MAP_WIDTH
	add l
	ld l, a
	ret

.lo_map_addresses
	db $a3 ; 00
	db $a6 ; 01
	db $a9 ; 02
	db $ac ; 03
	db $af ; 04
	db $03 ; 05
	db $06 ; 06
	db $09 ; 07
	db $0c ; 08
	db $0f ; 09
	db $63 ; 10
	db $66 ; 11
	db $69 ; 12
	db $6c ; 13
	db $6f ; 14
	db $c3 ; 15
	db $c6 ; 16
	db $c9 ; 17
	db $cc ; 18
	db $cf ; 19

_TreasureCollection:
	call ProcessDPadRepeat
	call HandleTreasureCollectionInput
	call AnimateTreasureCollection
	call ClearUnusedVirtualOAM
	ret

HandleTreasureCollectionInput:
	ld a, [wJoypadPressed]
	bit B_BUTTON_F, a
	jr z, .asm_9a646

; b btn
	stop_music2
	stop_sfx
	ld a, $08
	ld [wSubState], a
	ret

.asm_9a646
	ld hl, wCollectionPage
	ld a, [wJoypadDown]
	ld b, a
	bit A_BUTTON_F, b
	jr z, .no_a_btn
	ld a, [wCollectionScrollMode]
	and a
	call z, .SetScrollMode
	ld hl, wCollectionPage
	bit D_RIGHT_F, b
	jr nz, .a_right
	bit D_LEFT_F, b
	jr nz, .a_left
	ret

.SetScrollMode:
	ld a, TRUE
	ld [wCollectionScrollMode], a
	ld hl, wSceneObj9State
	ld a, $03
	call SetSceneObjState
	ld hl, wSceneObj10State
	ld a, $04
	call SetSceneObjState
	ret

.a_right
	ld a, $04
	cp [hl]
	jr z, .asm_9a682
	inc [hl]
	jr .asm_9a68f
.asm_9a682
	ld [hl], $00
	jr .asm_9a68f
.a_left
	ld a, [hl]
	and a
	jr z, .asm_9a68d
	dec [hl]
	jr .asm_9a68f
.asm_9a68d
	ld [hl], $04
.asm_9a68f
	play_sfx SFX_0EB
	stop_music2
	ld a, $0c
	ld [wSubState], a
	ret

.no_a_btn
	xor a
	ld [wCollectionScrollMode], a
	ld [wSceneObj9State], a
	ld [wSceneObj10State], a

	ld a, [wJoypadPressed]
	ld b, a
	ld hl, wCollectionCol
	ld a, [hl]
	bit D_RIGHT_F, b
	jr nz, .right
	bit D_LEFT_F, b
	jr nz, .left
	ld hl, wCollectionRow
	ld a, [hl]
	bit D_UP_F, b
	jr nz, .up
	bit D_DOWN_F, b
	jr nz, .down
	call ProcessCollectionLinkState
	ret

.right
	cp NUM_COLLECTION_COLS - 1
	jr z, .page_right
	inc [hl]
	ld a, [wCollectionCell]
	inc a
	jr .got_target_cell
.left
	and a
	jr z, .page_left
	dec [hl]
	ld a, [wCollectionCell]
	dec a
	jr .got_target_cell
.down
	cp NUM_COLLECTION_ROWS - 1
	jr nc, .cant_scroll
	inc [hl]
	ld a, [wCollectionCell]
	add NUM_COLLECTION_COLS
	jr .got_target_cell
.up
	and a
	jr z, .cant_scroll
	dec [hl]
	ld a, [wCollectionCell]
	sub NUM_COLLECTION_COLS
.got_target_cell
	ld [wCollectionCell], a
	play_sfx SFX_0E2

	xor a
	ld [wSceneObj2State], a
	ld [wSceneObj3State], a
	ld [wSceneObj4State], a
	ld [wSceneObj5State], a
	ld [wSceneObj6State], a
	ld [wSceneObj7State], a
	ld [wSceneObj8State], a

	ld de, wCollectionLinkState
	xor a
	ld [de], a ; wCollectionLinkState
	inc e
	ld [de], a ; wCollectionLinkStateCounter
	jr GetCollectionCellCoords

.cant_scroll
	play_sfx SFX_0E5
	ret

.page_right
	ld [hl], $00
	ld a, [wCollectionCell]
	sub NUM_COLLECTION_ROWS
	ld [wCollectionCell], a
	ld hl, wCollectionPage
	inc [hl]
	ld a, [hl]
	cp NUM_COLLECTION_PAGES
	jr nz, .change_page
	ld [hl], $00
	jr .change_page

.page_left
	ld [hl], NUM_COLLECTION_ROWS
	ld a, [wCollectionCell]
	add NUM_COLLECTION_ROWS
	ld [wCollectionCell], a
	ld hl, wCollectionPage
	dec [hl]
	ld a, [hl]
	cp -1
	jr nz, .change_page
	ld [hl], NUM_COLLECTION_PAGES - 1

.change_page
	play_sfx SFX_0EB
	ld a, $0c
	ld [wSubState], a
	ret

GetCollectionCellCoords:
	ld a, [wCollectionRow]
	ld hl, .y_coords
	ld c, a
	ld b, $00
	add hl, bc
	ld a, [hl]
	ld [wSceneObj1YCoord], a
	ld a, [wCollectionCol]
	ld hl, .x_coords
	ld c, a
	add hl, bc
	ld a, [hl]
	ld [wSceneObj1XCoord], a
	ret

.y_coords
	db $30 ; row 0
	db $48 ; row 1
	db $60 ; row 2
	db $78 ; row 3

.x_coords
	db $20 ; col 0
	db $38 ; col 1
	db $50 ; col 2
	db $68 ; col 3
	db $80 ; col 4

Func_9a78e:
	ld a, [de]
	ld hl, .table_1
	ld c, a
	ld b, $00
	add hl, bc
	ld a, [hl]
	ld [de], a
	inc e
	ld a, [de]
	ld hl, .table_2
	ld c, a
	add hl, bc
	ld a, [hl]
	ld [de], a
	ret

.table_1
	db $30 ; row 0
	db $48 ; row 1
	db $60 ; row 2
	db $78 ; row 3

.table_2
	db $20 ; col 0
	db $38 ; col 1
	db $50 ; col 2
	db $68 ; col 3
	db $80 ; col 4

GetCurrentCollectionCellRowAndColumn:
	ld a, [wCollectionCell]
	ld hl, wCollectionRow
;	fallthrough

GetCollectionCellRowAndColumn:
	ld b, 0
.loop_find_row
	cp NUM_COLLECTION_COLS
	jr c, .found_row
	sub NUM_COLLECTION_COLS
	inc b
	jr .loop_find_row
.found_row
	ld [hl], b ; row
	inc l
	ld [hli], a ; col
	ret

ProcessCollectionLinkState:
	ld hl, wCollectionLinkStateCounter
	inc [hl]
	ld b, h
	ld c, l

	ld a, [wCollectionLinkState]
	jumptable
	dw .AdvanceState
	dw .WaitDelay
	dw .GetLinkedCells
	dw .Return
	dw .Func_9a806
	dw .Return

.AdvanceState:
	ld hl, wCollectionLinkStateCounter
	xor a
	ld [hld], a
	inc [hl] ; wCollectionLinkState
	ret

.WaitDelay:
	ld a, [bc]
	cp $14
	ret c
	jr .AdvanceState

.GetLinkedCells:
	ld hl, CollectionLinkTable
	ld a, [wCollectionPage]
	call GetPointerFromTableHL
	ld a, [wCollectionCell]
	call GetPointerFromTableHL
	ld a, [hl]
	cp $80
	jr z, .AdvanceState
	ld b, 7
	ld de, wCollectionLinkedCells
	call CopyHLToDE
	call .AdvanceState
	jp .AdvanceState

.Return:
	ret

.Func_9a806:
	ld a, [wCollectionLinkedCells + 0]
	cp $80
	jr z, .AdvanceState
	dec a
	ld hl, wCollectionCell
	cp [hl]
	jr z, .asm_9a823
	ld hl, wSceneObj2YCoord
	call GetCollectionCellRowAndColumn
	call .Func_9a8d8
	ld de, wSceneObj2YCoord
	call Func_9a78e
.asm_9a823
	ld a, [wCollectionLinkedCells + 1]
	cp $80
	jr z, .AdvanceState
	dec a
	ld hl, wCollectionCell
	cp [hl]
	jr z, .asm_9a840
	ld hl, wSceneObj3YCoord
	call GetCollectionCellRowAndColumn
	call .Func_9a8d8
	ld de, wSceneObj3YCoord
	call Func_9a78e
.asm_9a840
	ld a, [wCollectionLinkedCells + 2]
	cp $80
	jr z, .AdvanceState
	dec a
	ld hl, wCollectionCell
	cp [hl]
	jr z, .asm_9a85d
	ld hl, wSceneObj3End
	call GetCollectionCellRowAndColumn
	call .Func_9a8d8
	ld de, wSceneObj3End
	call Func_9a78e
.asm_9a85d
	ld a, [wCollectionLinkedCells + 3]
	cp $80
	jp z, .AdvanceState
	dec a
	ld hl, wCollectionCell
	cp [hl]
	jr z, .asm_9a87b
	ld hl, wSceneObj4End
	call GetCollectionCellRowAndColumn
	call .Func_9a8d8
	ld de, wSceneObj4End
	call Func_9a78e
.asm_9a87b
	ld a, [wCollectionLinkedCells + 4]
	cp $80
	jp z, .AdvanceState
	dec a
	ld hl, wCollectionCell
	cp [hl]
	jr z, .asm_9a899
	ld hl, wSceneObj5End
	call GetCollectionCellRowAndColumn
	call .Func_9a8d8
	ld de, wSceneObj5End
	call Func_9a78e
.asm_9a899
	ld a, [wCollectionLinkedCells + 5]
	cp $80
	jp z, .AdvanceState
	dec a
	ld hl, wCollectionCell
	cp [hl]
	jr z, .asm_9a8b7
	ld hl, wSceneObj6End
	call GetCollectionCellRowAndColumn
	call .Func_9a8d8
	ld de, wSceneObj6End
	call Func_9a78e
.asm_9a8b7
	ld a, [wCollectionLinkedCells + 6]
	cp $80
	jp z, .AdvanceState
	dec a
	ld hl, wCollectionCell
	cp [hl]
	jr z, .asm_9a8d5
	ld hl, wSceneObj7End
	call GetCollectionCellRowAndColumn
	call .Func_9a8d8
	ld de, wSceneObj7End
	call Func_9a78e
.asm_9a8d5
	jp .AdvanceState

.Func_9a8d8:
	set 2, l
	ld a, $02
	call SetSceneObjState
	ret

CollectionLinkTable:
	dw .Page0
	dw .Page1
	dw .Page2
	dw .Page3
	dw .Page4

.none
	db $80

.Page0:
	dw .music_boxes ; YELLOW_MUSIC_BOX
	dw .music_boxes ; BLUE_MUSIC_BOX
	dw .music_boxes ; GREEN_MUSIC_BOX
	dw .music_boxes ; RED_MUSIC_BOX
	dw .music_boxes ; GOLD_MUSIC_BOX
	dw .none ; PRINCE_FROGS_GLOVES_T
	dw .none ; SWIMMING_FLIPPERS_T
	dw .none ; HIGH_JUMP_BOOTS_T
	dw .none ; SUPER_GRAB_GLOVES_T
	dw .none ; GARLIC_T
	dw .none ; GRAB_GLOVE_T
	dw .none ; LEAD_OVERALLS_T
	dw .none ; SUPER_JUMP_SLAM_OVERALLS_T
	dw .none ; HEAD_SMASH_HELMET_T
	dw .lantern_and_flame ; LANTERN
	dw .lantern_and_flame ; MAGICAL_FLAME
	dw .none ; TORCH
	dw .gears ; GEAR_1
	dw .gears ; GEAR_2
	dw .none ; WARP_COMPACT

.music_boxes
	db $01, $02, $03, $04, $05, $80
.lantern_and_flame
	db $0f, $10, $80
.gears
	db $12, $13, $80

.Page1:
	dw .none ; JAR
	dw .none ; TREASURE_MAP
	dw .book_and_magic_wand ; BLUE_BOOK
	dw .none ; SKY_KEY
	dw .book_and_trident ; YELLOW_BOOK
	dw .book_and_trident ; TRIDENT
	dw .none ; AXE
	dw .book_and_magic_wand ; MAGIC_WAND
	dw .skull_rings ; SKULL_RING_BLUE
	dw .skull_rings ; SKULL_RING_RED
	dw .tablets ; BLUE_TABLET
	dw .tablets ; GREEN_TABLET
	dw .none ; ORNAMENTAL_FAN
	dw .scroll ; TOP_HALF_OF_SCROLL
	dw .scroll ; BOTTOM_HALF_OF_SCROLL
	dw .tusks_and_flower ; TUSK_BLUE
	dw .tusks_and_flower ; TUSK_RED
	dw .tusks_and_flower ; GREEN_FLOWER
	dw .chemicals ; BLUE_CHEMICAL
	dw .chemicals ; RED_CHEMICAL

.book_and_magic_wand
	db $03, $08, $80
.book_and_trident
	db $05, $06, $80
.skull_rings
	db $09, $0a, $80
.tablets
	db $0b, $0c, $80
.scroll
	db $0e, $0f, $80
.tusks_and_flower
	db $10, $11, $12, $80
.chemicals
	db $13, $14, $80

.Page2:
	dw .none ; AIR_PUMP
	dw .none ; SAPLING_OF_GROWTH
	dw .none ; NIGHT_VISION_SCOPE
	dw .none ; ELECTRIC_FAN_PROPELLER
	dw .none ; RUST_SPRAY
	dw .none ; STATUE
	dw .none ; EXPLOSIVE_PLUNGER_BOX
	dw .none ; SCISSORS
	dw .none ; CASTLE_BRICK
	dw .none ; WARP_REMOVAL_APPARATUS
	dw .key_cards ; KEY_CARD_RED
	dw .key_cards ; KEY_CARD_BLUE
	dw .none ; JACKHAMMER
	dw .none ; PICK_AXE
	dw .none ; ROCKET
	dw .none ; POCKET_PET
	dw .none ; MYSTERY_HANDLE
	dw .none ; DEMONS_BLOOD
	dw .none ; GOLD_MAGIC
	dw .none ; FIGHTER_MANNEQUIN

.key_cards
	db $0b, $0c, $80

.Page3:
	dw .none ; TRUCK_WHEEL
	dw .none ; FLUTE
	dw .none ; FOOT_OF_STONE
	dw .golden_eyes ; GOLDEN_RIGHT_EYE
	dw .golden_eyes ; GOLDEN_LEFT_EYE
	dw .glass_eyes ; RIGHT_GLASS_EYE
	dw .glass_eyes ; LEFT_GLASS_EYE
	dw .none ; SCEPTER
	dw .sun_medallion ; SUN_MEDALLION_TOP
	dw .sun_medallion ; SUN_MEDALLION_BOTTOM
	dw .storm_and_pouch ; EYE_OF_THE_STORM
	dw .none ; MAGIC_SEEDS
	dw .storm_and_pouch ; POUCH
	dw .none ; FULL_MOON_GONG
	dw .none ; TELEPHONE
	dw .none ; CROWN
	dw .none ; DAY_OR_NIGHT_SPELL
	dw .none ; RED_GEM
	dw .none ; GREEN_GEM
	dw .none ; BLUE_GEM

.golden_eyes
	db $04, $05, $80
.glass_eyes
	db $06, $07, $80
.sun_medallion
	db $09, $0a, $80
.storm_and_pouch
	db $0b, $0d, $80

.Page4:
	dw .none ; CLUBS_CREST
	dw .none ; SPADES_CREST
	dw .none ; HEART_CREST
	dw .none ; DIAMONDS_CREST
	dw .none ; EARTHEN_FIGURE
	dw .none ; SABER
	dw .none ; GOBLET
	dw .none ; TEAPOT
	dw .none ; MAGNIFYING_GLASS
	dw .none ; UFO
	dw .none ; MINICAR
	dw .none ; LOCOMOTIVE
	dw .none ; FIRE_DRENCHER
	dw .crayons ; CRAYON_RED_T
	dw .crayons ; CRAYON_BROWN_T
	dw .crayons ; CRAYON_YELLOW_T
	dw .crayons ; CRAYON_GREEN_T
	dw .crayons ; CRAYON_CYAN_T
	dw .crayons ; CRAYON_BLUE_T
	dw .crayons ; CRAYON_PINK_T

.crayons
	db $0e, $0f, $10, $11, $12, $13, $14, $80

Func_9a9ec:
	ld h, b
	ld l, c
	res 1, l
	ld b, $26
	call UpdateOWAnimation
	ld a, [bc]
	ret

Func_9a9f7:
	ld b, $26
	call LoadOWSpriteWithScroll
	ret

Func_9a9fd:
	ld a, [wOWAnimationFinished]
	cp $ff
	ret nz
	set 2, l
	xor a
	ld [hli], a
	ld [hli], a
	ld a, e
	ld [hl], a
	ld b, h
	ld c, l
	ret

AnimateTreasureCollection:
	ld bc, wSceneObj1State
	call .Func_9aa4a
	ld bc, wSceneObj2State
	call .Func_9aa4a
	ld bc, wSceneObj3State
	call .Func_9aa4a
	ld bc, wSceneObj4State
	call .Func_9aa4a
	ld bc, wSceneObj5State
	call .Func_9aa4a
	ld bc, wSceneObj6State
	call .Func_9aa4a
	ld bc, wSceneObj7State
	call .Func_9aa4a
	ld bc, wSceneObj8State
	call .Func_9aa4a
	ld bc, wSceneObj9State
	call .Func_9aa4a
	ld bc, wSceneObj10State
	call .Func_9aa4a
	ret

.Func_9aa4a:
	ld a, [bc]
	jumptable

	dw Func_9aa6c
	dw Func_9aa6d
	dw Func_9aa72
	dw Func_9aa77
	dw Func_9aa7c

Func_9aa56:
	call Func_9a9ec
Func_9aa59:
	ld de, OAM_9ac5d
	call Func_9a9f7
	ret

; unreferenced
Func_9aa60:
	call Func_9a9ec
	ld e, $00
	call Func_9a9fd
	jr nz, Func_9aa59
	jr AnimateTreasureCollection.Func_9aa4a

Func_9aa6c:
	ret

Func_9aa6d:
	ld de, Frameset_9ace1
	jr Func_9aa56

Func_9aa72:
	ld de, Frameset_9acea
	jr Func_9aa56

Func_9aa77:
	ld de, Frameset_9acf3
	jr Func_9aa56

Func_9aa7c:
	ld de, Frameset_9acfc
	jr Func_9aa56

; input:
; c = * constant
GetLevelThatContainsTreasure:
	ld b, -1
	ld hl, LevelTreasureIDs
.loop
	inc b
	ld a, [hli]
	cp c
	jr nz, .loop
	srl b
	srl b ; /4
	ret

LoadLevelTreasureData::
	push_wram BANK("WRAM2")
	call ClearTreasureData
	call LoadLevelTreasures
	call LoadTreasurePals
	call LoadTreasureTiles

	xor a ; VRAM0
	ldh [rVBK], a
	ld hl, wTileBuffer
	ld de, v0Tiles1 tile $10
	ld b, $00 ; $100 bytes = 10 tiles
	call CopyHLToDE
	pop_wram
	ret

Func_9aab5:
	ld a, [wMapSideLevelID]
	cp OW_EXITS
	ret nc

	call ClearTreasureData
	call Func_9aaf4
	call Func_9ab1c

	ld a, [wGameModeFlags]
	bit MODE_TIME_ATTACK_F, a
	jp nz, GetLevelTimeAttackScore

	call LoadLevelTreasures
	call FillBottomBarTreasureIDs
	call WriteUncollectedBottomBarTreasureIDs
	call LoadTreasurePals
	call LoadTreasureTiles

	ld a, HIGH(wTileBuffer)
	ld [wHDMASourceHi], a
	ld a, LOW(wTileBuffer)
	ld [wHDMASourceLo], a
	ld a, $09
	ld [wHDMADestHi], a
	xor a
	ld [wHDMADestLo], a
	ld a, $10 dma_tiles
	ld [wHDMAMode], a
	ret

Func_9aaf4:
	ld a, [wOWLevel]
	call CheckLevelMusicalCoinFlag
	ret z ; didn't get all musical coins
	ld hl, wBottomBarFlags
	set 6, [hl]
	ld hl, wCompassObjState
	xor a
	ld [hli], a
	ld [hl], a ; wCompassObjUnk7
	ret

; clears treasure tiles and info in WRAM
ClearTreasureData:
; clear the treasure tiles
	xor a
	ld hl, wTileBuffer
	ld bc, 16 tiles
	call WriteAToHL_BCTimes

; clear the treasure info
	xor a
	ld hl, wLevelTreasureIDs
	ld bc, $10
	call WriteAToHL_BCTimes
	ret

Func_9ab1c:
	ld a, [wOWLevel]
	and a
	ret z
	cp $80
	ret z
	farcall GetOWLevelCollectedTreasures
	ret

LoadTreasureTiles:
	ld a, [wLevelGreyTreasureID]
	ld de, wTileBuffer
	call .CopyGfx
	ld a, [wLevelRedTreasureID]
	call .CopyGfx
	ld a, [wLevelGreenTreasureID]
	call .CopyGfx
	ld a, [wLevelBlueTreasureID]
	call .CopyGfx
	; bug, fallthrough

.CopyGfx:
; this gets right addr given that a < 128
	and a
	ld b, $00
	rla
	rla
	rl b
	ld c, a
	and $f0
	or b
	swap a
	ld b, a
	ld a, $0f
	and c
	swap a
	ld c, a
	; bc = a * 4 tiles
	ld hl, TreasureGfx
	add hl, bc
	ld b, 4 tiles
	call CopyHLToDE
	ret

; for each treasure that hasn't been collected
; in the current level, write in [de]
; a value of $00 (TREASURE_NONE)
WriteUncollectedBottomBarTreasureIDs:
	ld a, [wKeyAndTreasureFlags]
	swap a
	and $0f
	ld b, a
	ld c, NUM_LEVEL_TREASURES
	ld de, wLevelTreasureIDs
.loop_level_treasures
	srl b
	jr c, .next
	xor a ; TREASURE_NONE
	ld [de], a
.next
	inc e
	dec c
	jr nz, .loop_level_treasures
	ret

; loads in wLevelTreasureIDs the treasure IDs for level in wOWLevel
LoadLevelTreasures:
	ld hl, LevelTreasureIDs
	ld de, wLevelTreasureIDs
	call .GetLevelTreasures
	ret ; can be fallthrough

.GetLevelTreasures:
	ld a, [wOWLevel]
	add a
	add a ; *4
	ld c, a
	ld b, $00
	add hl, bc
	ld b, NUM_LEVEL_TREASURES
.loop
	ld a, [hli]
	ld [de], a
	inc e
	dec b
	jr nz, .loop
	ret

LoadTreasurePals:
	ld de, TreasureOBPals
	ld a, [wLevelGreyTreasureID]
	ld l, a
	ld h, $00
	add hl, de
	ld a, [hl]
	ld [wLevelGreyTreasurePal], a

	ld a, [wLevelRedTreasureID]
	ld l, a
	ld h, $00
	add hl, de
	ld a, [hl]
	ld [wLevelRedTreasurePal], a

	ld a, [wLevelGreenTreasureID]
	ld l, a
	ld h, $00
	add hl, de
	ld a, [hl]
	ld [wLevelGreenTreasurePal], a

	ld a, [wLevelBlueTreasureID]
	ld l, a
	ld h, $00
	add hl, de
	ld a, [hl]
	ld [wLevelBlueTreasurePal], a
	ret

GetLevelTimeAttackScore:
	ld a, [wOWLevel]
	and a
	ret z
	cp $80
	ret z
	ld hl, wLevelTimeAttackScores
	dec a
	ld de, w2d0ee
	add a ; *2
	ld c, a
	ld b, $00
	add hl, bc
	ld a, [hli]
	ld [de], a ; w2d0ee
	inc e
	ld a, [hl]
	ld [de], a ; w2d0ef
	ret

; fills wBottomBarTreasureIDs
; with treasures in wLevelTreasureIDs
FillBottomBarTreasureIDs:
	ld de, wBottomBarTreasureIDs
	ld hl, wLevelTreasureIDs
	ld b, NUM_LEVEL_TREASURES
	call CopyHLToDE
	ret

TreasureOBPals:
	table_width 1
	db OBPAL_TREASURE_YELLOW ; TREASURE_NONE
	db OBPAL_TREASURE_YELLOW ; YELLOW_MUSIC_BOX
	db OBPAL_TREASURE_BLUE   ; BLUE_MUSIC_BOX
	db OBPAL_TREASURE_GREEN  ; GREEN_MUSIC_BOX
	db OBPAL_TREASURE_RED    ; RED_MUSIC_BOX
	db OBPAL_TREASURE_YELLOW ; GOLD_MUSIC_BOX
	db OBPAL_TREASURE_GREEN  ; PRINCE_FROGS_GLOVES_T
	db OBPAL_TREASURE_GREEN  ; SWIMMING_FLIPPERS_T
	db OBPAL_TREASURE_BLUE   ; HIGH_JUMP_BOOTS_T
	db OBPAL_TREASURE_YELLOW ; SUPER_GRAB_GLOVES_T
	db OBPAL_TREASURE_YELLOW ; GARLIC_T
	db OBPAL_TREASURE_RED    ; GRAB_GLOVE_T
	db OBPAL_TREASURE_BLUE   ; LEAD_OVERALLS_T
	db OBPAL_TREASURE_RED    ; SUPER_JUMP_SLAM_OVERALLS_T
	db OBPAL_TREASURE_GREEN  ; HEAD_SMASH_HELMET_T
	db OBPAL_TREASURE_YELLOW ; LANTERN
	db OBPAL_TREASURE_RED    ; MAGICAL_FLAME
	db OBPAL_TREASURE_RED    ; TORCH
	db OBPAL_TREASURE_BLUE   ; GEAR_1
	db OBPAL_TREASURE_BLUE   ; GEAR_2
	db OBPAL_TREASURE_BLUE   ; WARP_COMPACT
	db OBPAL_TREASURE_BLUE   ; JAR
	db OBPAL_TREASURE_YELLOW ; TREASURE_MAP
	db OBPAL_TREASURE_BLUE   ; BLUE_BOOK
	db OBPAL_TREASURE_YELLOW ; SKY_KEY
	db OBPAL_TREASURE_YELLOW ; YELLOW_BOOK
	db OBPAL_TREASURE_GREEN  ; TRIDENT
	db OBPAL_TREASURE_BLUE   ; AXE
	db OBPAL_TREASURE_YELLOW ; MAGIC_WAND
	db OBPAL_TREASURE_BLUE   ; SKULL_RING_BLUE
	db OBPAL_TREASURE_RED    ; SKULL_RING_RED
	db OBPAL_TREASURE_BLUE   ; BLUE_TABLET
	db OBPAL_TREASURE_GREEN  ; GREEN_TABLET
	db OBPAL_TREASURE_RED    ; ORNAMENTAL_FAN
	db OBPAL_TREASURE_YELLOW ; TOP_HALF_OF_SCROLL
	db OBPAL_TREASURE_YELLOW ; BOTTOM_HALF_OF_SCROL
	db OBPAL_TREASURE_GREEN  ; TUSK_BLUE
	db OBPAL_TREASURE_GREEN  ; TUSK_RED
	db OBPAL_TREASURE_GREEN  ; GREEN_FLOWER
	db OBPAL_TREASURE_BLUE   ; BLUE_CHEMICAL
	db OBPAL_TREASURE_RED    ; RED_CHEMICAL
	db OBPAL_TREASURE_YELLOW ; AIR_PUMP
	db OBPAL_TREASURE_YELLOW ; SAPLING_OF_GROWTH
	db OBPAL_TREASURE_GREEN  ; NIGHT_VISION_SCOPE
	db OBPAL_TREASURE_BLUE   ; ELECTRIC_FAN_PROPELLER
	db OBPAL_TREASURE_RED    ; RUST_SPRAY
	db OBPAL_TREASURE_RED    ; STATUE
	db OBPAL_TREASURE_BLUE   ; EXPLOSIVE_PLUNGER_BOX
	db OBPAL_TREASURE_BLUE   ; SCISSORS
	db OBPAL_TREASURE_RED    ; CASTLE_BRICK
	db OBPAL_TREASURE_GREEN  ; WARP_REMOVAL_APPARATUS
	db OBPAL_TREASURE_RED    ; KEY_CARD_RED
	db OBPAL_TREASURE_BLUE   ; KEY_CARD_BLUE
	db OBPAL_TREASURE_GREEN  ; JACKHAMMER
	db OBPAL_TREASURE_YELLOW ; PICK_AXE
	db OBPAL_TREASURE_YELLOW ; ROCKET
	db OBPAL_TREASURE_YELLOW ; POCKET_PET
	db OBPAL_TREASURE_BLUE   ; MYSTERY_HANDLE
	db OBPAL_TREASURE_RED    ; DEMONS_BLOOD
	db OBPAL_TREASURE_YELLOW ; GOLD_MAGIC
	db OBPAL_TREASURE_BLUE   ; FIGHTER_MANNEQUIN
	db OBPAL_TREASURE_BLUE   ; TRUCK_WHEEL
	db OBPAL_TREASURE_YELLOW ; FLUTE
	db OBPAL_TREASURE_GREEN  ; FOOT_OF_STONE
	db OBPAL_TREASURE_YELLOW ; GOLDEN_RIGHT_EYE
	db OBPAL_TREASURE_YELLOW ; GOLDEN_LEFT_EYE
	db OBPAL_TREASURE_BLUE   ; RIGHT_GLASS_EYE
	db OBPAL_TREASURE_BLUE   ; LEFT_GLASS_EYE
	db OBPAL_TREASURE_BLUE   ; SCEPTER
	db OBPAL_TREASURE_RED    ; SUN_MEDALLION_TOP
	db OBPAL_TREASURE_RED    ; SUN_MEDALLION_BOTTOM
	db OBPAL_TREASURE_BLUE   ; EYE_OF_THE_STORM
	db OBPAL_TREASURE_GREEN  ; MAGIC_SEEDS
	db OBPAL_TREASURE_BLUE   ; POUCH
	db OBPAL_TREASURE_YELLOW ; FULL_MOON_GONG
	db OBPAL_TREASURE_GREEN  ; TELEPHONE
	db OBPAL_TREASURE_RED    ; CROWN
	db OBPAL_TREASURE_RED    ; DAY_OR_NIGHT_SPELL
	db OBPAL_TREASURE_RED    ; RED_GEM
	db OBPAL_TREASURE_GREEN  ; GREEN_GEM
	db OBPAL_TREASURE_BLUE   ; BLUE_GEM
	db OBPAL_TREASURE_GREEN  ; CLUBS_CREST
	db OBPAL_TREASURE_YELLOW ; SPADES_CREST
	db OBPAL_TREASURE_RED    ; HEART_CREST
	db OBPAL_TREASURE_BLUE   ; DIAMONDS_CREST
	db OBPAL_TREASURE_YELLOW ; EARTHEN_FIGURE
	db OBPAL_TREASURE_BLUE   ; SABER
	db OBPAL_TREASURE_BLUE   ; GOBLET
	db OBPAL_TREASURE_YELLOW ; TEAPOT
	db OBPAL_TREASURE_BLUE   ; MAGNIFYING_GLASS
	db OBPAL_TREASURE_BLUE   ; UFO
	db OBPAL_TREASURE_RED    ; MINICAR
	db OBPAL_TREASURE_YELLOW ; LOCOMOTIVE
	db OBPAL_TREASURE_RED    ; FIRE_DRENCHER
	db OBPAL_TREASURE_RED    ; CRAYON_RED_T
	db OBPAL_TREASURE_YELLOW ; CRAYON_BROWN_T
	db OBPAL_TREASURE_YELLOW ; CRAYON_YELLOW_T
	db OBPAL_TREASURE_GREEN  ; CRAYON_GREEN_T
	db OBPAL_TREASURE_BLUE   ; CRAYON_CYAN_T
	db OBPAL_TREASURE_BLUE   ; CRAYON_BLUE_T
	db OBPAL_TREASURE_RED    ; CRAYON_PINK_T
	db $03                   ; TREASURE_DUMMY
	assert_table_length NUM_TREASURES + 1

OAM_9ac5d:
	dw .frame_0
	dw .frame_1
	dw .frame_2
	dw .frame_3
	dw .frame_4
	dw .frame_5
	dw .frame_6
	dw .frame_7
	dw .frame_8
	dw .frame_9
	dw .frame_10
	dw .frame_11

.frame_0
	frame_oam -23, -11, $00, 0
	frame_oam  -7, -11, $02, 0
	frame_oam -23,   3, $00, 0 | OAMF_XFLIP
	frame_oam  -7,   3, $02, 0 | OAMF_XFLIP
	db $80

.frame_1
	frame_oam -22, -11, $04, 0
	frame_oam  -6, -11, $06, 0
	frame_oam -22,   3, $04, 0 | OAMF_XFLIP
	frame_oam  -6,   3, $06, 0 | OAMF_XFLIP
	db $80

.frame_2
	frame_oam -21, -11, $08, 0
	frame_oam  -5, -11, $0a, 0
	frame_oam -21,   3, $08, 0 | OAMF_XFLIP
	frame_oam  -5,   3, $0a, 0 | OAMF_XFLIP
	db $80

.frame_3
	frame_oam  -8, -11, $0c, 0
	frame_oam  -8,   3, $0c, 0 | OAMF_XFLIP
	db $80

.frame_4
	frame_oam  -8, -11, $0e, 0
	frame_oam  -8,   3, $0e, 0 | OAMF_XFLIP
	db $80

.frame_5
	frame_oam  -8, -11, $10, 1
	frame_oam  -8,   3, $10, 1 | OAMF_XFLIP
	db $80

.frame_6
	frame_oam  -8,  -4, $12, 2
	db $80

.frame_7
	frame_oam  -8,  -4, $14, 2
	db $80

.frame_8
	frame_oam  -8,  -4, $16, 2
	db $80

.frame_9
	frame_oam  -8,  -4, $12, 2 | OAMF_XFLIP
	db $80

.frame_10
	frame_oam  -8,  -4, $14, 2 | OAMF_XFLIP
	db $80

.frame_11
	frame_oam  -8,  -4, $16, 2 | OAMF_XFLIP
	db $80

Frameset_9ace1::
	db   0,  3
	db   1,  3
	db   2,  3
	db   1,  3
	db $ff

Frameset_9acea::
	db   3,  6
	db   4,  6
	db   5,  6
	db   4,  6
	db $ff

Frameset_9acf3::
	db   6,  8
	db   7,  4
	db   8,  8
	db   7,  4
	db $ff

Frameset_9acfc::
	db   9,  8
	db  10,  4
	db  11,  8
	db  10,  4
	db $ff

Pals_9ad05:
	rgb 31, 31, 31
	rgb 10, 31, 10
	rgb  0, 20,  0
	rgb  0,  0,  0

	rgb 31, 31, 31
	rgb 10, 31, 10
	rgb 20, 31, 20
	rgb  0,  0,  7

	rgb 31, 31, 31
	rgb 15, 25, 31
	rgb  0, 15, 31
	rgb  0,  0,  0

	rgb 31, 31, 31
	rgb 25, 25, 25
	rgb 15, 15, 15
	rgb  0,  0,  7

	rgb 31, 31, 31
	rgb 25, 25, 25
	rgb 15, 15, 15
	rgb  0,  0,  7

	rgb 31, 31, 31
	rgb 25, 25, 25
	rgb 15, 15, 15
	rgb  0,  0,  7

	rgb 31, 31, 31
	rgb 25, 25, 25
	rgb 15, 15, 15
	rgb  0,  0,  7

	rgb 31, 31, 31
	rgb 25, 25, 25
	rgb 15, 15, 15
	rgb  0,  0,  7

Pals_9ad45:
	rgb 31, 31, 31
	rgb 20, 31, 31
	rgb 10, 20, 31
	rgb  0,  0,  0

	rgb 31, 31, 31
	rgb 31, 31, 31
	rgb 31, 10,  0
	rgb  0,  0,  0

	rgb 31, 31, 31
	rgb 20,  0,  0
	rgb 31, 15,  0
	rgb  0,  0,  0

	rgb 31, 31, 31
	rgb 31, 31,  0
	rgb 31,  4, 13
	rgb  0,  0,  0

TreasurePals::
TreasureYellowPal:
; OBPAL_TREASURE_YELLOW
	rgb 31, 31, 31
	rgb 31, 31,  0
	rgb 19, 16,  0
	rgb  5,  4,  0

TreasureRedPal:
; OBPAL_TREASURE_RED
	rgb 31, 31, 31
	rgb 31, 19, 23
	rgb 27,  0,  6
	rgb  7,  0,  0

TreasureGreenPal:
; OBPAL_TREASURE_GREEN
	rgb 31, 31, 31
	rgb 22, 31, 20
	rgb  0, 21,  0
	rgb  0,  2,  0

TreasureBluePal:
; OBPAL_TREASURE_BLUE
	rgb 31, 31, 31
	rgb  2, 31, 31
	rgb  6,  6, 31
	rgb  0,  0,  7
